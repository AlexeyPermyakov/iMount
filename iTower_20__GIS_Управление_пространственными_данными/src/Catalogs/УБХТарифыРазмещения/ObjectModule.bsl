#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ВидТарифа = Перечисления.УБХВидыТарифовРазмещения.Стандартный
		ИЛИ (ДополнительныеСвойства.Свойство("КэшВидаТариф") 
		И НЕ ДополнительныеСвойства.КэшВидаТариф = ВидТарифа) Тогда
		ЗаписатьДанныеПоКлиентскойГруппе();
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьДанныеПоКлиентскойГруппе()

	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.УБХСтандартныеТарифыКлиентскихГрупп");
	ЭлементБлокировки.УстановитьЗначение("КлиентскаяГруппа", КлиентскаяГруппа);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать();
 
	НаборЗаписей = РегистрыСведений.УБХСтандартныеТарифыКлиентскихГрупп.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КлиентскаяГруппа.Установить(КлиентскаяГруппа);
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	
	Если ВидТарифа = Перечисления.УБХВидыТарифовРазмещения.Стандартный
		И ЗначениеЗаполнено(КлиентскаяГруппа) Тогда
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.КлиентскаяГруппа = КлиентскаяГруппа;
		НоваяЗапись.ТарифРазмещения  = Ссылка;
	КонецЕсли;
	
	НаборЗаписей.Записать();

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ВидТарифа = Перечисления.УБХВидыТарифовРазмещения.Стандартный Тогда
		НепроверяемыеРеквизиты = Новый Массив;
		НепроверяемыеРеквизиты.Добавить("Проект");
		ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли
