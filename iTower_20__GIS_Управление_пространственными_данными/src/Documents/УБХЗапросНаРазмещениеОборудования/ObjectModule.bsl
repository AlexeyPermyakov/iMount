#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущаяВерсияЗапроса) Тогда
		ВерсияОбъект = Документы.УБХВерсияЗапросаНаРазмещение.СоздатьДокумент();
		ВерсияОбъект.Дата = ТекущаяДата();
		ВерсияОбъект.Записать();
		ТекущаяВерсияЗапроса = ВерсияОбъект.Ссылка;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА
	
	// Инициализация дополнительных свойств для проведения документа
	УБХПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	Документы.УБХЗапросНаРазмещениеОборудования.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);

	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ИНФОРМАЦИОННОЙ БАЗЫ
	
	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ
	УБХЗапросыРазмещения.ОтразитьЗапросыКлиентовПБК(ДополнительныеСвойства, Движения, Отказ);
	УБХЗапросыРазмещения.ОтразитьОборудованиеОбъектаПБК(ДополнительныеСвойства, Движения, Отказ);
	
	// ЗАПОЛНИТЬ ВЕРСИЮ ЗАПРОСА
	УБХЗапросыРазмещения.АктуализироватьДанныеВерсииЗапроса(ДополнительныеСвойства, Отказ);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.УБХВерсияЗапросаНаРазмещение") Тогда
		
		ЗаполнитьПоВерсииЗапросаНаРазмещение(ДанныеЗаполнения, ДанныеЗаполнения);
		
	КонецЕсли;

	Если ЭтотОбъект.ЭтоНовый() Тогда
		ИнициализироватьДокумент(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоВерсииЗапросаНаРазмещение(Знач ДокументОснование, ДанныеЗаполнения)
	
	ДанныеЗаполнения = Новый Структура;
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВерсияЗапросаНаРазмещение.ПереченьОборудования.(
		|		Ссылка КАК Ссылка,
		|		НомерСтроки КАК НомерСтроки,
		|		ТипРазмещенияВКонтейнере КАК ТипРазмещенияВКонтейнере,
		|		ЕдиницаОборудования КАК ЕдиницаОборудования,
		|		Статус КАК Статус,
		|		ГруппаРазмещения КАК ГруппаРазмещения,
		|		ПлановаяВысотаПодвеса КАК ПлановаяВысотаПодвеса,
		|		Габариты КАК Габариты,
		|		Диаметр КАК Диаметр,
		|		Площадь КАК Площадь,
		|		Азимут КАК Азимут,
		|		ДлинаКабеля КАК ДлинаКабеля,
		|		КоличествоНитокКабеля КАК КоличествоНитокКабеля,
		|		НомерМеста КАК НомерМеста,
		|		КоличествоМодулей КАК КоличествоМодулей
		|	) КАК ПереченьОборудования,
		|	ВерсияЗапросаНаРазмещение.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.УБХВерсияЗапросаНаРазмещение КАК ВерсияЗапросаНаРазмещение
		|ГДЕ
		|	ВерсияЗапросаНаРазмещение.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументОснование);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Если Не ВыборкаДетальныеЗаписи.ПереченьОборудования.Пустой() Тогда 
			ПереченьОборудования.Загрузить(ВыборкаДетальныеЗаписи.ПереченьОборудования.Выгрузить());
		Иначе
			ПереченьОборудования.Очистить();
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры	

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	Статус 		= Справочники.УБХУБХСтатусыРазмещенияОбъектаПБК.Черновик;
	ТипЗапроса 	= Перечисления.УБХТипыЗапросовРазмещения.НовоеРазмещение;
КонецПроцедуры

#КонецОбласти

#КонецЕсли
