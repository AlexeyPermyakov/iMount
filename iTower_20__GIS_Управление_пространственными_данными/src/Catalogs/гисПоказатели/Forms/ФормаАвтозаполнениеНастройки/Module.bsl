#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Дата = Параметры.Дата;
	Если Не ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДата();
	КонецЕсли;
	
	ЦветНачала = Новый Цвет(255, 0, 0);
	Элементы.ВыбратьЦветНачала.ЦветФона = ЦветНачала;
	ЦветКонца = Новый Цвет(0, 255, 0);
	Элементы.ВыбратьЦветКонца.ЦветФона = ЦветКонца;
	
	Если Не Параметры.Показатель.ТипЗначения = Перечисления.гисПоказателиТипы.Число Тогда
		Элементы.ЗначениеМин.Видимость = Ложь;
		Элементы.ЗначениеМакс.Видимость = Ложь;
		Элементы.КоличествоГрадаций.Видимость = Ложь;
	Иначе
		ЗаполнитьМинМакс();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	ЗаполнитьМинМакс();
КонецПроцедуры


&НаКлиенте
Процедура ВыбратьЦветНачала(Команда)
	ОткрытьФорму("ОбщаяФорма.гисФормаВыбораЦветаRGB", Новый Структура("Цвет", ЦветНачала), Элементы.ВыбратьЦветНачала, , , , 
		Новый ОписаниеОповещения("ВыбратьЦветНачалаЗавершение", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьЦветНачалаЗавершение(ВыбраннныйЦвет, ДополнительныеПараметры) Экспорт
	Если ТипЗнч(ВыбраннныйЦвет) = Тип("Цвет") Тогда
		ЦветНачала = ВыбраннныйЦвет;
		Элементы.ВыбратьЦветНачала.ЦветФона = ВыбраннныйЦвет;
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура ВыбратьЦветКонца(Команда)
	ОткрытьФорму("ОбщаяФорма.гисФормаВыбораЦветаRGB", Новый Структура("Цвет", ЦветКонца), Элементы.ВыбратьЦветКонца, , , , 
		Новый ОписаниеОповещения("ВыбратьЦветКонцаЗавершение", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьЦветКонцаЗавершение(ВыбраннныйЦвет, ДополнительныеПараметры) Экспорт
	Если ТипЗнч(ВыбраннныйЦвет) = Тип("Цвет") Тогда
		ЦветКонца = ВыбраннныйЦвет;
		Элементы.ВыбратьЦветКонца.ЦветФона = ВыбраннныйЦвет;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПрименитьИзменения(Команда)
	Если Элементы.КоличествоГрадаций.Видимость И Не ЗначениеЗаполнено(КоличествоГрадаций) Тогда
		ПоказатьПредупреждение(, "Не заполнено количество градаций!");
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект.Закрыть(Новый Структура("ЗначениеМин,ЗначениеМакс,КоличествоГрадаций,ЦветНачала,ЦветКонца", ЗначениеМин, ЗначениеМакс, КоличествоГрадаций, ЦветНачала, ЦветКонца));
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьМинМакс()
	Если Параметры.Показатель.ТипЗначения <> Перечисления.гисПоказателиТипы.Число Тогда
		Возврат;
	КонецЕсли;
	
	СписокОбъектов = Справочники.гисСлоиКарты.ПолучитьСписокОбъектов(Параметры.СлойКарты);
	
	Запрос = Новый Запрос("ВЫБРАТЬ Минимум(ВЫРАЗИТЬ(ЗначениеЧисло КАК Число)) КАК Мин, Максимум(ВЫРАЗИТЬ(ЗначениеЧисло КАК Число)) КАК Макс ИЗ РегистрСведений.гисЗначенияПоказателейОбъектов.СрезПоследних(&ДатаСреза, Показатель = &Показатель И Объект В (&СписокОбъектов))");
	Запрос.УстановитьПараметр("ДатаСреза", Дата);
	Запрос.УстановитьПараметр("Показатель", Параметры.Показатель);
	Запрос.УстановитьПараметр("СписокОбъектов", СписокОбъектов);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗначениеМин = Выборка.Мин;
		ЗначениеМакс = Выборка.Макс;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
