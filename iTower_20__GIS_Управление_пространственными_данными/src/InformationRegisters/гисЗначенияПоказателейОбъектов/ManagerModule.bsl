#Область ПрограммныйИнтерфейс

// Возвращает значение объекта по показателю на дату.
//
// Параметры:
//  Объект		 - СправочникСсылка.гисОбъектыКарты - объект, значение которого нужно получить;
//  Показатель	 - СправочникСсылка.гисПоказатели - показатель, значение которого нужно получить;
//  Дата		 - Дата - дата, на которую нужно получить значение.
// 
// Возвращаемое значение:
//   - Число - если значение показателя числовое;
//   - Булево - если значение показателя типа булево;
//   - СправочникСсылка - если значение показателя - справочник.
//
Функция ПолучитьЗначениеОбъектаПоПоказателю(Объект, Показатель, Дата) Экспорт
	Если Показатель.ТипЗначения = Перечисления.гисПоказателиТипы.Булево Тогда
		Значение = Ложь;
		НазваниеРеквизита = "ЗначениеБулево";
	ИначеЕсли Показатель.ТипЗначения = Перечисления.гисПоказателиТипы.Число Тогда
		Значение = 0;
		НазваниеРеквизита = "ЗначениеЧисло";
	ИначеЕсли Показатель.ТипЗначения = Перечисления.гисПоказателиТипы.Справочник Тогда
		Значение = Справочники[Показатель.СправочникИмя].ПустаяСсылка();
		НазваниеРеквизита = "ЗначениеСправочник";
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ * ИЗ РегистрСведений.гисЗначенияПоказателейОбъектов.СрезПоследних(&Дата, Объект = &Объект И Показатель = &Показатель И Период >= &ДатаНачала)");
	
	СтрокиСлоев = Показатель.СлоиКарты.НайтиСтроки(Новый Структура("СлойКарты", Объект.Слой));
	Если СтрокиСлоев.Количество() > 0 Тогда
		СтрокаСлоев = СтрокиСлоев[0];
		Если ЗначениеЗаполнено(СтрокаСлоев.ШаблонРасчета) Тогда
			Запрос.Текст = СтрокаСлоев.ШаблонРасчета;
			НазваниеРеквизита = "Значение";
		КонецЕсли;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Объект", Объект);
	Запрос.УстановитьПараметр("Показатель", Показатель);
	
	ДатаНачала = Справочники.гисПоказатели.ПолучитьНачалоПериода(Показатель, Дата);
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	
	Попытка
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ЗначениеНеПустое = Выборка[НазваниеРеквизита];
			Если ТипЗнч(Значение) <> ТипЗнч(ЗначениеНеПустое) Тогда
				ОписаниеТипа = Справочники.гисПоказатели.ПолучитьТипЗначения(Показатель.ТипЗначения, Показатель.СправочникИмя);
				ЗначениеНеПустое = ОписаниеТипа.ПривестиЗначение(ЗначениеНеПустое);
			КонецЕсли;
			
			Значение = ЗначениеНеПустое;
		КонецЕсли;
	Исключение
		ВызватьИсключение "Ошибка при расчете значения показателя " + Показатель + " для объекта " + Объект + " на дату " + Дата;
	КонецПопытки;
	
	Возврат Значение;
КонецФункции

#КонецОбласти
