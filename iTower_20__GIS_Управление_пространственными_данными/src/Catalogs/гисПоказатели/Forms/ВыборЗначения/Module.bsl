#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	НоваяСтрока = ДеревоЭлементов.ПолучитьЭлементы().Добавить();
	НоваяСтрока.Значение = "Объект";
	НоваяСтрока.Синоним = "Объект";
	
	ТипСлоя = Неопределено;
	Если ЗначениеЗаполнено(Параметры.ТипСлояКарты) Тогда
		ТипСлоя = Параметры.ТипСлояКарты;
	ИначеЕсли ЗначениеЗаполнено(Параметры.СлойКарты) Тогда
		ТипСлоя = Параметры.СлойКарты.ТипСлоя;
	КонецЕсли;
	
	Если ТипСлоя = Перечисления.гисТипыСлоев.ПоСправочникуОбъектыКарты Тогда
		НоваяСтрока.Тип = Новый ОписаниеТипов("СправочникСсылка.гисОбъектыКарты");
	ИначеЕсли ТипСлоя = Перечисления.гисТипыСлоев.ПоСправочникуКадастровыеЗемельныеУчастки Тогда
		НоваяСтрока.Тип = Новый ОписаниеТипов("СправочникСсылка.гисКадастровыеЗемельныеУчастки");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.СлойКарты.СправочникОбъектовУчета) Тогда
		ЗаполнитьНаСервере(0, Параметры.СлойКарты.СправочникОбъектовУчета);
	Иначе
		НоваяПодчиненнаяСтрока = НоваяСтрока.ПолучитьЭлементы().Добавить();
	КонецЕсли;
	
	Если Параметры.Тематика Тогда
		НоваяСтрока = ДеревоЭлементов.ПолучитьЭлементы().Добавить();
		НоваяСтрока.Значение = "Показатель";
		НоваяСтрока.Синоним = "Показатель";
		НоваяСтрока.Тип = Новый ОписаниеТипов("СправочникСсылка.гисПоказатели");
		
		НоваяПодчиненнаяСтрока = НоваяСтрока.ПолучитьЭлементы().Добавить();
		
		НоваяСтрока = ДеревоЭлементов.ПолучитьЭлементы().Добавить();
		НоваяСтрока.Значение = "Значение";
		НоваяСтрока.Синоним = "Значение";
		НоваяСтрока.Тип = Новый ОписаниеТипов("Число");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Значение) Тогда
		МассивИменОбъектов = СтрРазделить(Параметры.Значение, ".");
		ТекущаяКоллекция = ДеревоЭлементов.ПолучитьЭлементы();
		ИскомоеЗначение = "";
		Пока МассивИменОбъектов.Количество() > 0 Цикл
			ИскомоеЗначение = ИскомоеЗначение + ?(ИскомоеЗначение = "", "", ".") + МассивИменОбъектов[0];
			
			Если ТекущаяКоллекция.Количество() = 1 И ТекущаяКоллекция[0].Значение = "" Тогда
				ЗаполнитьНаСервере(ТекущаяКоллекция[0].ПолучитьРодителя().ПолучитьИдентификатор());
			КонецЕсли;
			
			ЗначениеНайдено = Ложь;
			Для Каждого СтрокаКоллекции Из ТекущаяКоллекция Цикл 
				Если СтрокаКоллекции.Значение = ИскомоеЗначение Тогда
					ТекущаяКоллекция = СтрокаКоллекции.ПолучитьЭлементы();
					НачальныйИдентификаторСтроки = СтрокаКоллекции.ПолучитьИдентификатор();
					МассивИменОбъектов.Удалить(0);
					ЗначениеНайдено = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если Не ЗначениеНайдено Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ЗначениеЗаполнено(НачальныйИдентификаторСтроки) Тогда
		Элементы.ДеревоЭлементов.ТекущаяСтрока = НачальныйИдентификаторСтроки;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоЭлементов

&НаКлиенте
Процедура ДеревоЭлементовПередРазворачиванием(Элемент, Строка, Отказ)
	ТекущаяСтрока = ДеревоЭлементов.НайтиПоИдентификатору(Строка);
	
	Если ТекущаяСтрока.ПолучитьЭлементы().Количество() = 1 И 
		ТекущаяСтрока.ПолучитьЭлементы()[0].Значение = "" Тогда
		
		ЗаполнитьНаСервере(Строка);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоЭлементовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ВыбратьЗначение(Команды.ВыбратьЗначение)
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьЗначение(Команда)
	ЭтотОбъект.Закрыть(Элементы.ДеревоЭлементов.ТекущиеДанные.Значение);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция СсылочныйТип(Тип)
	ХотьОдинПодходит = Ложь;
	НомерТипа = 0;
	Пока Не ХотьОдинПодходит И НомерТипа < Тип.Типы().Количество() Цикл
		ПроверяемыйТип = Тип.Типы()[НомерТипа];
		
		Если Справочники.ТипВсеСсылки().СодержитТип(ПроверяемыйТип) Тогда
			ХотьОдинПодходит = Истина;
		КонецЕсли;
		Если Документы.ТипВсеСсылки().СодержитТип(ПроверяемыйТип) Тогда
			ХотьОдинПодходит = Истина;
		КонецЕсли;
		
		НомерТипа = НомерТипа + 1;
	КонецЦикла;
	
	Возврат ХотьОдинПодходит;
КонецФункции

&НаСервере
Процедура ЗаполнитьНаСервере(ИдентификаторТекущейСтроки, ИмяСлояДляОбъектаУчета = Неопределено)
	ТекущаяСтрока = ДеревоЭлементов.НайтиПоИдентификатору(ИдентификаторТекущейСтроки);
	ТекущаяСтрока.ПолучитьЭлементы().Очистить();
	
	ОбъектыМетаданных = Новый Массив;
	// справочники
	Для Каждого ОбъектМетаданных Из Метаданные.Справочники Цикл
		Если ТекущаяСтрока.Тип.СодержитТип(Тип("СправочникСсылка." + ОбъектМетаданных.Имя)) Тогда
			ОбъектыМетаданных.Добавить(ОбъектМетаданных);
		КонецЕсли;
	КонецЦикла;
	// документы
	Для Каждого ОбъектМетаданных Из Метаданные.Документы Цикл
		Если ТекущаяСтрока.Тип.СодержитТип(Тип("ДокументСсылка." + ОбъектМетаданных.Имя)) Тогда
			ОбъектыМетаданных.Добавить(ОбъектМетаданных);
		КонецЕсли;
	КонецЦикла;
	
	МассивИменРеквизитов = Новый Массив;
	
	Для Каждого ОбъектМетаданных Из ОбъектыМетаданных Цикл
		Для Каждого Реквизит Из ОбъектМетаданных.СтандартныеРеквизиты Цикл
			Если Реквизит.Имя = "Ссылка" Тогда
				Продолжить;
			КонецЕсли;
			
			Если МассивИменРеквизитов.Найти(Реквизит.Имя) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			МассивИменРеквизитов.Добавить(Реквизит.Имя);
			
			НоваяСтрока = ТекущаяСтрока.ПолучитьЭлементы().Добавить();
			НоваяСтрока.Значение = ТекущаяСтрока.Значение + ?(ТекущаяСтрока.Значение = "", "", ".") + Реквизит.Имя;
			НоваяСтрока.Синоним = ?(ЗначениеЗаполнено(Реквизит.Синоним), Реквизит.Синоним, Реквизит.Имя);
			НоваяСтрока.Тип = Реквизит.Тип;
			
			Если Реквизит.Тип.Типы().Количество() > 5 Тогда
				Продолжить;
			ИначеЕсли СсылочныйТип(Реквизит.Тип) Тогда
				НоваяПустаяСтрокаДляГруппы = НоваяСтрока.ПолучитьЭлементы().Добавить();
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	МассивИменРеквизитов = Новый Массив;
	Для Каждого ОбъектМетаданных Из ОбъектыМетаданных Цикл
		Для Каждого Реквизит Из ОбъектМетаданных.Реквизиты Цикл
			Если Реквизит.Имя = "Ссылка" Тогда
				Продолжить;
			КонецЕсли;
			
			Если МассивИменРеквизитов.Найти(Реквизит.Имя) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			МассивИменРеквизитов.Добавить(Реквизит.Имя);
			
			НоваяСтрока = ТекущаяСтрока.ПолучитьЭлементы().Добавить();
			НоваяСтрока.Значение = ТекущаяСтрока.Значение + ?(ТекущаяСтрока.Значение = "", "", ".") + Реквизит.Имя;
			НоваяСтрока.Синоним = ?(ЗначениеЗаполнено(Реквизит.Синоним), Реквизит.Синоним, Реквизит.Имя);
			НоваяСтрока.Тип = Реквизит.Тип;
			
			Если Реквизит.Имя = "ОбъектУчета" И ИмяСлояДляОбъектаУчета <> Неопределено Тогда
				НоваяСтрока.Тип = Новый ОписаниеТипов("СправочникСсылка." + ИмяСлояДляОбъектаУчета);
			КонецЕсли;
			
			Если НоваяСтрока.Тип.Типы().Количество() > 5 Тогда
				Продолжить;
			ИначеЕсли СсылочныйТип(НоваяСтрока.Тип) Тогда
				НоваяПустаяСтрокаДляГруппы = НоваяСтрока.ПолучитьЭлементы().Добавить();
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти
