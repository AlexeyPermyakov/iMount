
#Область ПрограммныйИнтерфейс

Процедура ПриСозданииФормыЗадачиНаСервере(Форма, РасширенныеНастройки) Экспорт
	
КонецПроцедуры

Процедура СкопироватьБизнесПроцессПоЗаявке_ПриЗаполненииДанныхПроцесса(ДанныеПроцесса, СтруктураДанных, Заявка, ДополнительныеПараметры) Экспорт
	
// Подсистема СтандартныеПодсистемы.РаботаСФайлами
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСФайлами") Тогда
		текКопироватьФайлы = Ложь;
		Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
		   И ДополнительныеПараметры.Свойство("КопироватьФайлы") Тогда
			текКопироватьФайлы = (ДополнительныеПараметры.КопироватьФайлы = Истина);
		КонецЕсли;
		
		МодульРаботаСФайламиСлужебный = ОбщегоНазначения.ОбщийМодуль("РаботаСФайламиСлужебный");
		
		Если текКопироватьФайлы Тогда
			// Получим данные файлов и подготовим данные для их создания
			текБизнесПроцесс = Заявка.БизнесПроцесс;
			ИмяСправочникаХранилищаФайлов = МодульРаботаСФайламиСлужебный.ИмяСправочникаХраненияФайлов(текБизнесПроцесс);
			
			текЗапрос = Новый Запрос;
			текЗапрос.Текст =
			"ВЫБРАТЬ
			|	спрФайлы.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.лм_БизнесПроцессПрисоединенныеФайлы КАК спрФайлы
			|ГДЕ
			|	спрФайлы.ВладелецФайла = &ВладелецФайла";
			текЗапрос.УстановитьПараметр("ВладелецФайла",	текБизнесПроцесс);
			
			текЗапрос.Текст = СтрЗаменить(текЗапрос.Текст, "Справочник.лм_БизнесПроцессПрисоединенныеФайлы", "Справочник." + ИмяСправочникаХранилищаФайлов);
			
			// Вопрос: как создать ссылки на новые файлы?
			мсвКопируемыеФайлы = текЗапрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
			
			ДополнительныеПараметры.Вставить("КопируемыеФайлы",	мсвКопируемыеФайлы);
		Иначе
			// Найдем ссылки на файлы и очистим их
			текБизнесПроцесс = Заявка.БизнесПроцесс;
			ИмяСправочникаХранилищаФайлов	= МодульРаботаСФайламиСлужебный.ИмяСправочникаХраненияФайлов(текБизнесПроцесс);
			отПрисоединенныйФайл			= Новый ОписаниеТипов("СправочникСсылка." + ИмяСправочникаХранилищаФайлов);
			отСтруктура						= Новый ОписаниеТипов("Структура");
			
			Для Каждого сткРеквизит Из СтруктураДанных.Реквизиты Цикл
				Если сткРеквизит.ТипЗначения = отПрисоединенныйФайл Тогда
					Если ДанныеПроцесса.Свойство(сткРеквизит.ИмяПоля) Тогда
						ДанныеПроцесса[сткРеквизит.ИмяПоля] = Неопределено;
					КонецЕсли;
				ИначеЕсли сткРеквизит.ТипЗначения = отСтруктура И ДанныеПроцесса.Свойство(сткРеквизит.ИмяПоля) Тогда
					сткОтбор = Новый Структура("ОсновнойИдентификатор", сткРеквизит.Идентификатор);
					мсвРеквизитыСтруктуры = лм_ОбщегоНазначенияКлиентСервер.НайтиСтрокиТаблицыОбъекта(СтруктураДанных.Реквизиты, сткОтбор);
					
					ДанныеСтруктурыПроцесса = ДанныеПроцесса[сткРеквизит.ИмяПоля];
					Для Каждого ДанныеЭлементаСтруктурыПроцесса Из ДанныеСтруктурыПроцесса Цикл
						Для Каждого сткРеквизитСтруктуры Из мсвРеквизитыСтруктуры Цикл
							Если сткРеквизитСтруктуры.ТипЗначения = отПрисоединенныйФайл Тогда
								Если ДанныеЭлементаСтруктурыПроцесса.Свойство(сткРеквизитСтруктуры.ИмяПоля) Тогда
									ДанныеЭлементаСтруктурыПроцесса[сткРеквизитСтруктуры.ИмяПоля] = Неопределено;
								КонецЕсли;
							КонецЕсли;
						КонецЦикла;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
// Конец Подсистема СтандартныеПодсистемы.РаботаСФайлами

// Подсистема кбп_ВедениеУникодов
	Для Каждого кзРеквизитПроцесса Из ДанныеПроцесса Цикл
		Если СтрСравнить(кзРеквизитПроцесса.Ключ, "Уникод") = 0 Тогда
			// Очистим Уникод
			ДанныеПроцесса[кзРеквизитПроцесса.Ключ] = "";
		КонецЕсли;
	КонецЦикла;
// Конец Подсистема кбп_ВедениеУникодов

// Подсистема кбп_ВедениеGUID
	Для Каждого кзРеквизитПроцесса Из ДанныеПроцесса Цикл
		Если СтрСравнить(кзРеквизитПроцесса.Ключ, "кбп_GUID") = 0 Тогда
			// Очистим GUID
			ДанныеПроцесса[кзРеквизитПроцесса.Ключ] = "";
		КонецЕсли;
	КонецЦикла;
// Конец Подсистема кбп_ВедениеGUID

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийОбъекта

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

