&НаСервереБезКонтекста
Функция УстановитьДоступныеОтборы(Родитель)
	
	ДоступныеЗначения = Новый СписокЗначений;
	
	ПериодичностьРодителя = Родитель.Периодичность;
	Для каждого ЗначениеПеречисления Из Перечисления.Периодичность Цикл
		ТекПериодичностьУжеРодительской = НЕ УБХОбщегоНазначенияВызовСервера.СравнитьПериодичность(ЗначениеПеречисления, ПериодичностьРодителя, Истина);
		Если ТекПериодичностьУжеРодительской Тогда
			ДоступныеЗначения.Добавить(ЗначениеПеречисления);
		Иначе
			// Периодичность шире родительской. Не добавляем в список.
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДоступныеЗначения;
	
КонецФункции

&НаСервере
Процедура ОбработатьИзменениеПараметров()
	
	Если НЕ ЗначениеЗаполнено(Объект.ДатаНачала) Тогда
		Возврат; // отказ
	ИначеЕсли НЕ ЗначениеЗаполнено(Объект.Периодичность) Тогда
		Возврат; // отказ
	КонецЕсли;
	
	Если Объект.Периодичность = Перечисления.Периодичность.Год Тогда
		УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.ДатаНачала,    НачалоГода(Объект.ДатаНачала));
		Справочники.Периоды.ОпределитьДатуОкончания(Объект.ДатаНачала, Объект.ДатаОкончания, Объект.Периодичность);
		УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.Наименование, ПредставлениеПериода(НачалоДня(Объект.ДатаНачала), КонецДня(Объект.ДатаОкончания)));
		Объект.Код = Лев(Объект.Наименование, 4);
		
	ИначеЕсли Объект.Периодичность = Перечисления.Периодичность.ДевятьМесяцев Тогда
		УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.ДатаНачала,   НачалоГода(Объект.ДатаНачала));
		УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.Наименование, "Девять месяцев " + Формат(Объект.ДатаНачала, "ДФ='гггг ""г.""'"));
		Справочники.Периоды.ОпределитьДатуОкончания(Объект.ДатаНачала, Объект.ДатаОкончания, Объект.Периодичность);
		
	ИначеЕсли Объект.Периодичность = Перечисления.Периодичность.Полугодие Тогда
		Если Объект.ДатаНачала < ДобавитьМесяц(НачалоГода(Объект.ДатаНачала), 6) Тогда
			УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.ДатаНачала, НачалоГода(Объект.ДатаНачала));
			УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.Наименование, "1 полугодие " + Формат(Объект.ДатаНачала, "ДФ='гггг ""г.""'"));
		Иначе 
			УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.ДатаНачала, ДобавитьМесяц(НачалоГода(Объект.ДатаНачала), 6));
			УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.Наименование, "2 полугодие " + Формат(Объект.ДатаНачала, "ДФ='гггг ""г.""'"));
		КонецЕсли;
		Справочники.Периоды.ОпределитьДатуОкончания(Объект.ДатаНачала, Объект.ДатаОкончания, Объект.Периодичность);
		
	ИначеЕсли Объект.Периодичность = Перечисления.Периодичность.Квартал Тогда
		УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.ДатаНачала,    НачалоКвартала(Объект.ДатаНачала));
		Справочники.Периоды.ОпределитьДатуОкончания(Объект.ДатаНачала, Объект.ДатаОкончания, Объект.Периодичность);
		УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.Наименование, ПредставлениеПериода(НачалоДня(Объект.ДатаНачала), КонецДня(Объект.ДатаОкончания)));
	ИначеЕсли Объект.Периодичность = Перечисления.Периодичность.Месяц Тогда
		УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.ДатаНачала,    НачалоМесяца(Объект.ДатаНачала));
		Справочники.Периоды.ОпределитьДатуОкончания(Объект.ДатаНачала, Объект.ДатаОкончания, Объект.Периодичность);
		УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.Наименование, ПредставлениеПериода(НачалоДня(Объект.ДатаНачала), КонецДня(Объект.ДатаОкончания)));
	ИначеЕсли Объект.Периодичность = Перечисления.Периодичность.Декада Тогда
		
		Если День(Объект.ДатаНачала) < 10 Тогда
			УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.ДатаНачала,    НачалоМесяца(Объект.ДатаНачала));
			УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.Наименование, "1 декада. " + Формат(Объект.ДатаНачала, "ДФ='ММММ гггг ""г.""'"));
		ИначеЕсли День(Объект.ДатаНачала) < 20 Тогда
			УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.ДатаНачала,    УБХОбщегоНазначения.ДобавитьДень(НачалоМесяца(Объект.ДатаНачала), 10));
			УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.Наименование, "2 декада. " + Формат(Объект.ДатаНачала, "ДФ='ММММ гггг ""г.""'"));
		Иначе 
			УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.ДатаНачала,    УБХОбщегоНазначения.ДобавитьДень(НачалоМесяца(Объект.ДатаНачала), 20));
			УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.Наименование, "3 декада. " + Формат(Объект.ДатаНачала, "ДФ='ММММ гггг ""г.""'"));
		КонецЕсли;
		Справочники.Периоды.ОпределитьДатуОкончания(Объект.ДатаНачала, Объект.ДатаОкончания, Объект.Периодичность);
	ИначеЕсли Объект.Периодичность = Перечисления.Периодичность.Неделя Тогда
		
		Если День(Объект.ДатаНачала) < 7 Тогда
			УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.ДатаНачала,    ?(День(НачалоНедели(Объект.ДатаНачала))>День(Объект.ДатаНачала),НачалоМесяца(Объект.ДатаНачала),НачалоНедели(Объект.ДатаНачала)));
			УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.Наименование, ?(УБХОбщегоНазначения.НачалоНеделиПоМесяцу(Объект.ДатаНачала)=НачалоМесяца(Объект.ДатаНачала),"1 неделя. ","2 неделя. ") + Формат(Объект.ДатаНачала, "ДФ='ММММ гггг ""г.""'"));
		ИначеЕсли День(Объект.ДатаНачала) < 14 Тогда
			УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.ДатаНачала,    НачалоНедели(Объект.ДатаНачала));
			УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.Наименование, ?(УБХОбщегоНазначения.НачалоНеделиПоМесяцу(УБХОбщегоНазначения.ДобавитьИнтервал(Объект.ДатаНачала,Перечисления.Периодичность.Неделя,-1))<=НачалоМесяца(Объект.ДатаНачала),"2 неделя. ","3 неделя. ") + Формат(Объект.ДатаНачала, "ДФ='ММММ гггг ""г.""'"));
		ИначеЕсли День(Объект.ДатаНачала) < 21 Тогда
			УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.ДатаНачала,    НачалоНедели(Объект.ДатаНачала));
			УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.Наименование, ?(УБХОбщегоНазначения.НачалоНеделиПоМесяцу(УБХОбщегоНазначения.ДобавитьИнтервал(Объект.ДатаНачала,Перечисления.Периодичность.Неделя,-2))<=НачалоМесяца(Объект.ДатаНачала),"3 неделя. ","4 неделя. ") + Формат(Объект.ДатаНачала, "ДФ='ММММ гггг ""г.""'"));
		ИначеЕсли День(Объект.ДатаНачала) < 28 Тогда
			УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.ДатаНачала,    НачалоНедели(Объект.ДатаНачала));
			УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.Наименование, ?(УБХОбщегоНазначения.НачалоНеделиПоМесяцу(УБХОбщегоНазначения.ДобавитьИнтервал(Объект.ДатаНачала,Перечисления.Периодичность.Неделя,-3))<=НачалоМесяца(Объект.ДатаНачала),"4 неделя. ","5 неделя. ") + Формат(Объект.ДатаНачала, "ДФ='ММММ гггг ""г.""'"));
		Иначе 
			УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.ДатаНачала,    НачалоНедели(Объект.ДатаНачала));
			УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.Наименование, ?(УБХОбщегоНазначения.НачалоНеделиПоМесяцу(УБХОбщегоНазначения.ДобавитьИнтервал(Объект.ДатаНачала,Перечисления.Периодичность.Неделя,-4))<=НачалоМесяца(Объект.ДатаНачала),"5 неделя. ","6 неделя. ") + Формат(Объект.ДатаНачала, "ДФ='ММММ гггг ""г.""'"));
		КонецЕсли;
		Справочники.Периоды.ОпределитьДатуОкончания(Объект.ДатаНачала, Объект.ДатаОкончания, Объект.Периодичность);
	
	Иначе
		Справочники.Периоды.ОпределитьДатуОкончания(Объект.ДатаНачала, Объект.ДатаОкончания, Объект.Периодичность);
		УБХОбщегоНазначения.УстановитьНовоеЗначение(Объект.Наименование, ПредставлениеПериода(НачалоДня(Объект.ДатаНачала), КонецДня(Объект.ДатаОкончания)));
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТолькоПросмотрПроизвольногоПериода()
	
	Элементы.Наименование.ТолькоПросмотр 	 = Не Объект.Произвольный;
	Элементы.ДатаОкончания.ТолькоПросмотр	 = Не Объект.Произвольный;
	Элементы.Периодичность.Видимость		 = НЕ Объект.Произвольный;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент)
	
	Если НЕ Объект.Произвольный Тогда
		ОбработатьИзменениеПараметров();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодичностьПриИзменении(Элемент)
	
	ОбработатьИзменениеПараметров();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьТолькоПросмотрПроизвольногоПериода();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодичностьНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = УстановитьДоступныеОтборы(Объект.Родитель);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		
		ТекОбъект = РеквизитФормыВЗначение("Объект");
		ТекОБъект.Заполнить(Неопределено);
		ЗначениеВРеквизитФормы(ТекОбъект, "Объект");
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПроизвольныйПриИзменении(Элемент)
	
	УстановитьТолькоПросмотрПроизвольногоПериода();
	
КонецПроцедуры
