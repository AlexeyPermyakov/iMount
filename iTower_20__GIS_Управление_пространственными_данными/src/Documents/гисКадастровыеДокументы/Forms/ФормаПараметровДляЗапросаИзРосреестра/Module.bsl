
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СортироватьПоПриИзменении(Элемент)
	СортироватьПоПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКадастровыеУчастки

&НаКлиенте
Процедура КадастровыеУчасткиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		ОтобразитьКоличествоСтрок();
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура КадастровыеУчасткиПослеУдаления(Элемент)
	ОтобразитьКоличествоСтрок();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПрименитьИзменения(Команда)
	МассивДанныхXml = Новый Массив;
	Для Каждого Строка Из КадастровыеУчастки Цикл
		Если Строка.ДанныеЗаполнены Тогда
			МассивДанныхXml.Добавить(Строка.ДанныеXml);
		КонецЕсли;
	КонецЦикла;

	Если МассивДанныхXml.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Нет данных по земельным участкам!'"));
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект.Закрыть(Новый Структура("МассивДанныхXml", МассивДанныхXml));
КонецПроцедуры


&НаКлиенте
Процедура ПолучитьКварталы(Команда)
	Если Не ЗначениеЗаполнено(КадастровыйКвартал) Тогда
		ПоказатьПредупреждение(, "Не заполнено начало кадастрового квартала!");
	Иначе
		АдресТекстДок = ПолучитьКварталыНаСервере();
		ТекстДок = ПолучитьИзВременногоХранилища(АдресТекстДок);
		ТекстДок.Показать("Кадастровые кварталы по запросу """ + КадастровыйКвартал + """");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьКварталыПример(Команда)
	ТекстПодсказки = "В отдельном окне выводится список номеров существующих 
	|кадастровых кварталов в пределах одного района/блока/квартала.
	|Пример запроса:
	|31:15:0507012 - один кадастровый квартал
	|31:15:0507% - список
	|31:15:% - все";
	ОткрытьФорму("ОбщаяФорма.гисФормаДляВыводаПодсказки", Новый Структура("Подсказка", ТекстПодсказки));
КонецПроцедуры


&НаКлиенте
Процедура ПолучитьЗУВсе(Команда)
	Если Не ЗначениеЗаполнено(КадастровыйНомерВсе) Тогда
		ПоказатьПредупреждение(, "Не заполнено начало кадастрового номера (все)!");
		Возврат;
	КонецЕсли;
	
	ПолучитьЗУВсеНаСервере();
	ОтобразитьКоличествоСтрок();
	СортироватьПо = 0;
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьЗУВсеПример(Команда)
	ТекстПодсказки = "Шаблон начала кадастрового номера (все участки).
	|Пример:
	|31:15:0507012:35 - один кадастровый участок
	|31:15:0507012:3% - список
	|31:15:0507012:% - все";
	ОткрытьФорму("ОбщаяФорма.гисФормаДляВыводаПодсказки", Новый Структура("Подсказка", ТекстПодсказки));
КонецПроцедуры


&НаКлиенте
Процедура ПолучитьКадастровыеДанные(Команда)
	ПолучитьКадастровыеДанныеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьКадастровыеДанныеПоВыделеннымЗУ(Команда)
	ПолучитьКадастровыеДанныеНаСервере(Элементы.КадастровыеУчастки.ВыделенныеСтроки);
КонецПроцедуры


&НаКлиенте
Процедура УдалитьСуществующиеЗУ(Команда)
	УдалитьСуществующиеЗУНаСервере();
	ОтобразитьКоличествоСтрок();
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзТабличногоДокумента(Команда)
	ОткрытьФорму("Документ.гисКадастровыеДокументы.Форма.ФормаЗагрузкиКадастровыхНомеров", , ЭтотОбъект, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьКварталыНаСервере()
	ТекстДок = Новый ТекстовыйДокумент;
	
	МассивДанных = гисРаботаСКадастром.ПолучитьКварталы(КадастровыйКвартал);
	Для Каждого ЭлементДанных Из МассивДанных Цикл
		ТекстДок.ДобавитьСтроку(ЭлементДанных.КадастровыйНомер);
	КонецЦикла;
	
	Возврат ПоместитьВоВременноеХранилище(ТекстДок, УникальныйИдентификатор);
КонецФункции

&НаСервере
Процедура ПолучитьЗУВсеНаСервере()
	МассивДанных = гисРаботаСКадастром.ПолучитьЗУВсе(КадастровыйНомерВсе);
	Для Каждого ЭлементДанных Из МассивДанных Цикл
		Строки = КадастровыеУчастки.НайтиСтроки(Новый Структура("КадастровыйНомер", ЭлементДанных.КадастровыйНомер));
		Если Строки.Количество() = 0 Тогда
			НоваяСтрока = КадастровыеУчастки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементДанных);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры


&НаСервере
Процедура ПолучитьКадастровыеДанныеПоСтроке(Строка)
	МассивДанныхXml = гисРаботаСКадастром.ЗаполнитьМассивДанныхИзРосреестра(Строка.КадастровыйНомер);
	Для Каждого ДанныеXml Из МассивДанныхXml Цикл
		Строка.Статус = Справочники.гисСтатусыКадастровыхУчастков.НайтиПоКоду(ДанныеXml.СтатусКод);
		Строка.КатегорияЗемель = Справочники.гисКатегорииЗемель.НайтиПоКоду(ДанныеXml.КатегорияЗемельКод);
		Строка.ВидИспользования = Справочники.гисВидыРазрешенногоИспользования.НайтиПоКоду(ДанныеXml.ВидИспользованияКод);
		Строка.ДанныеXml = гисРаботаСJson.ЗаписатьНативно(ДанныеXml);
		Строка.ДанныеЗаполнены = Истина;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПолучитьКадастровыеДанныеНаСервере(Знач Выделенные = Неопределено)
	Если Выделенные = Неопределено Тогда
		Для Каждого Строка Из КадастровыеУчастки Цикл
			Если Строка.ДанныеЗаполнены Тогда
				Продолжить;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Строка.КадастровыйНомер) Тогда
				ПолучитьКадастровыеДанныеПоСтроке(Строка);
			КонецЕсли;
		КонецЦикла;
	Иначе
		Для Каждого Идентификатор Из Выделенные Цикл
			Строка = КадастровыеУчастки.НайтиПоИдентификатору(Идентификатор);
			Если Строка <> Неопределено Тогда
				Строка.ДанныеЗаполнены = Ложь;
				Строка.ДанныеXml = "";
				Если ЗначениеЗаполнено(Строка.КадастровыйНомер) Тогда
					ПолучитьКадастровыеДанныеПоСтроке(Строка);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры


&НаСервере
Процедура УдалитьСуществующиеЗУНаСервере()
	МассивУдаляемыхСтрок = Новый Массив;
	Для Каждого Строка Из КадастровыеУчастки Цикл
		ЗУ = Справочники.гисКадастровыеЗемельныеУчастки.НайтиПоРеквизиту("КадастровыйНомер", Строка.КадастровыйНомер);
		Если Не ЗУ.Пустая() Тогда
			МассивУдаляемыхСтрок.Добавить(Строка);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Строка Из МассивУдаляемыхСтрок Цикл
		КадастровыеУчастки.Удалить(Строка);
	КонецЦикла;
КонецПроцедуры


&НаКлиенте
Процедура ОтобразитьКоличествоСтрок()
	КоличествоСтрок = КадастровыеУчастки.Количество();
КонецПроцедуры


&НаСервере
Процедура СортироватьПоПриИзмененииНаСервере()
	Если СортироватьПо = 1 Тогда
		КадастровыеУчастки.Сортировать("КадастровыйНомер");
	ИначеЕсли СортироватьПо = 2 Тогда
		КадастровыеУчастки.Сортировать("КатегорияЗемель");
	ИначеЕсли СортироватьПо = 3 Тогда
		КадастровыеУчастки.Сортировать("ВидИспользования");
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
