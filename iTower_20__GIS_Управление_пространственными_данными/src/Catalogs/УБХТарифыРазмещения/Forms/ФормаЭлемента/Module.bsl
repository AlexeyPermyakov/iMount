#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокПорогиВысотРазмещения, "Тариф", Объект.Ссылка);
	
	Если Параметры.Ключ.Пустая() Тогда
		ПодготовитьФормуНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	КэшВидаТариф = Объект.ВидТарифа;
	ПодготовитьФормуНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.ДополнительныеСвойства.Вставить("КэшВидаТариф", КэшВидаТариф);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВидТарифаПриИзменении(Элемент)
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры	

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	ЭтоСпецТариф = Форма.Объект.ВидТарифа = ПредопределенноеЗначение("Перечисление.УБХВидыТарифовРазмещения.Специальный");
	
	Форма.Элементы.КлиентскаяГруппа.Видимость 			= НЕ ЭтоСпецТариф;
	Форма.Элементы.СтатусСпециальногоТарифа.Видимость 	= ЭтоСпецТариф;
	Форма.Элементы.Проект.Видимость 					= ЭтоСпецТариф;

КонецПроцедуры

#КонецОбласти
