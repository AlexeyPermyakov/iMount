
#Область РаботаСоЗначениями

Процедура ПоказатьФормуРедактированияЗначения(Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка, Знач ИсходноеЗначение, Знач ОбработчикВводаЗначения, Оповещение) Экспорт
	
	ИмяФормыОбработчика = УправлениеЗначениямиВызовСервера.ЗначениеРеквизита(ОбработчикВводаЗначения, "ИмяФормыОбработчика");
	
	Если ПустаяСтрока(ИмяФормыОбработчика) Тогда
		Сообщить("Не указана форма обработчика.");
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = УправлениеЗначениямиВызовСервера.ПараметрыФормыОбработчика(ОбработчикВводаЗначения, ИсходноеЗначение);
	
	// Инициализация
	
	Попытка
		ОткрытьФорму(
			ИмяФормыОбработчика,
			ПараметрыФормы,
			, 
			, 
			, 
			, 
			Оповещение);
	Исключение
		Сообщить(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры

Процедура РедактированиеЗначенияЗавершениеВвода(Знач ПолученноеЗначение, Знач ДополнительныеПараметры) Экспорт
	
	// Проверка на значение
	Если НЕ ЗначениеЗаполнено(ПолученноеЗначение) Тогда
		Возврат;	
	КонецЕсли;
	
	Если ТипЗнч(ПолученноеЗначение) = Тип("Структура") 
			И НЕ ПустаяСтрока(ДополнительныеПараметры["ПолеВведенногоЗначения"]) Тогда
		ПолученноеЗначение = ПолученноеЗначение[ДополнительныеПараметры["ПолеВведенногоЗначения"]];
	КонецЕсли;
	
	РеквизитФормы = ?(ДополнительныеПараметры.ТекущиеДанныеТЧ = Неопределено,
						ДополнительныеПараметры.ФормаВладелец,
						ДополнительныеПараметры.ТекущиеДанныеТЧ);
	
	ПутьКРеквизитуФормы = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ДополнительныеПараметры.ИмяРеквизита, ".");
	// Если реквизит вида "Объект.Комментарий" и т.п.
	Если ПутьКРеквизитуФормы.Количество() > 1 Тогда
		Для Индекс = 0 По ПутьКРеквизитуФормы.Количество() - 2 Цикл 
			РеквизитФормы = РеквизитФормы[ПутьКРеквизитуФормы[Индекс]];
		КонецЦикла;
	КонецЕсли;	
	
	// Значение реквизита
	РеквизитФормы[ПутьКРеквизитуФормы[ПутьКРеквизитуФормы.Количество() - 1]] = ПолученноеЗначение;
			
	ДополнительныеПараметры.ФормаВладелец.Модифицированность = Истина;
	
	#Если ВебКлиент Тогда
		ДополнительныеПараметры.ФормаВладелец.ОбновитьОтображениеДанных();	
	#КонецЕсли
	
	Если ДополнительныеПараметры.ОповеститьФормуВладельца Тогда
		
		Оповестить("РедактированиеЗначения", , ДополнительныеПараметры.ФормаВладелец);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовФорм

Процедура ФормаСправочникаТаблицаЗначенийПриАктивизацииСтроки(Форма, Элемент, ИмяПоляЗначения) Экспорт
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Форма.Элементы[ИмяПоляЗначения].ТолькоПросмотр = Ложь;
	
	Если ТекущиеДанные = Неопределено 
		ИЛИ (НЕ ЗначениеЗаполнено(ТекущиеДанные.ТипЗначения))
		ИЛИ (НЕ ЗначениеЗаполнено(ТекущиеДанные.Атрибут))
	Тогда
		СтандартнаяОбработка = Ложь;
		Форма.Элементы[ИмяПоляЗначения].ТолькоПросмотр = Истина;
		Возврат;
	КонецЕсли;
	
	//Форма.Элементы[ИмяПоляЗначения].ОграничениеТипа = Новый ОписаниеТипов(ТекущиеДанные.ТипЗначения.Типы(),,);
	Форма.Элементы[ИмяПоляЗначения].ОграничениеТипа = Новый ОписаниеТипов(ТекущиеДанные.ТипЗначения);
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Атрибут)
			И ЗначениеЗаполнено(УправлениеЗначениямиВызовСервера.ЗначениеРеквизита(ТекущиеДанные.Атрибут, "ОбработчикЗаполненияЗначения")) Тогда
		Форма.Элементы[ИмяПоляЗначения].КнопкаВыбора = Истина;
	Иначе
		Форма.Элементы[ИмяПоляЗначения].КнопкаВыбора = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

Процедура ФормаСправочникаТаблицаЗначенийЗначениеНачалоВыбора(Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка, Атрибут, ТипЗначения, Оповещение) Экспорт
	
	Если ТипЗначения.ПривестиЗначение() = ПредопределенноеЗначение("Справочник.ЗначенияАтрибутов.ПустаяСсылка") Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Отбор", Новый Структура("Владелец", Атрибут));
		ПараметрыФормы.Вставить("РежимВыбора", Истина);
		
		ОткрытьФорму("Справочник.ЗначенияАтрибутов.ФормаСписка",
					   ПараметрыФормы,
					    Форма,,,,
						 Оповещение,
						  РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	Иначе
		
		ОбработчикЗаполненияЗначения = УправлениеЗначениямиВызовСервера.ЗначениеРеквизита(Атрибут, "ОбработчикЗаполненияЗначения");
		Если ЗначениеЗаполнено(ОбработчикЗаполненияЗначения) Тогда
			СтандартнаяОбработка = Ложь;
			
			УправлениеЗначениямиКлиент.ПоказатьФормуРедактированияЗначения(
				Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка,
				 Элемент.ТекстРедактирования, 
				  ОбработчикЗаполненияЗначения, 
				   Оповещение);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
