
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	рСтрока = 1;
	рСтолбец = 1;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИмяФайлаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	НачатьПомещениеФайла(Новый ОписаниеОповещения("ЗавершениеВыбораФайла", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеВыбораФайла(Результат, Адрес, ИмяВыбранногоФайла, ДополнительныеПараметры) Экспорт
	Если Результат Тогда
		АдресФайла = Адрес;
		ИмяФайла = ИмяВыбранногоФайла;
		ЗагрузитьЛисты();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьЗагрузку(Команда)
	Если Не ЗначениеЗаполнено(ИмяФайла) Тогда
		ПоказатьПредупреждение(, "Не выбран файл!");
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Лист) Тогда
		ПоказатьПредупреждение(, "Не выбран лист в файле!");
		Возврат;
	КонецЕсли;
	
	ВыполнитьЗагрузкуНаСервере();
	ПоказатьПредупреждение(, "Загрузка завершена!");
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВФорму(Команда)
	Для Каждого Строка Из рКадастровыеНомера Цикл
		НоваяСтрока = ВладелецФормы.КадастровыеУчастки.Добавить();
		НоваяСтрока.КадастровыйНомер = СокрЛП(Строка.КадастровыйНомер);
	КонецЦикла;
	ВладелецФормы.КоличествоСтрок = ВладелецФормы.КадастровыеУчастки.Количество();
	
	ВладелецФормы.Модифицированность = Истина;
	ЭтотОбъект.Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьЛисты()
	ИмяФайлаНаСервере = ИмяФайла;
	Файл = ПолучитьИзВременногоХранилища(АдресФайла);
	Файл.Записать(ИмяФайлаНаСервере);
	
	ТабДок = Новый ТабличныйДокумент;
	Попытка
		ТабДок.Прочитать(ИмяФайлаНаСервере); 
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	
	Элементы.Лист.СписокВыбора.Очистить();
	Для каждого Область ИЗ ТабДок.Области Цикл
		Элементы.Лист.СписокВыбора.Добавить(Область.Имя);
	КонецЦикла;
	
	Если Элементы.Лист.СписокВыбора.Количество() > 0 Тогда
		Лист = Элементы.Лист.СписокВыбора[0].Значение;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ВыполнитьЗагрузкуНаСервере()
	Если Не ЗначениеЗаполнено(ИмяФайлаНаСервере) Тогда
		ИмяФайлаНаСервере = ИмяФайла;
		Файл = ПолучитьИзВременногоХранилища(АдресФайла);
		Файл.Записать(ИмяФайлаНаСервере);
	КонецЕсли;
	
	ТабДок = Новый ТабличныйДокумент;
	Попытка
		ТабДок.Прочитать(ИмяФайлаНаСервере);
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	
	ЛистДокумента = ТабДок.ПолучитьОбласть(Лист);
	
	СтолбецНомер = рСтолбец;
	СтрокаНомер = рСтрока;
	КадастровыйНомер = ЛистДокумента.Область(СтрокаНомер, СтолбецНомер).Текст;
	Если Не ЗначениеЗаполнено(КадастровыйНомер) Тогда
		ОбщегоНазначения.СообщитьПользователю("Не найдено кадастровых номеров. Проверьте лист, строку и столбец, с которых начинается загрузка");
	Иначе
		Пока ЗначениеЗаполнено(КадастровыйНомер) Цикл
			НоваяСтрока = рКадастровыеНомера.Добавить();
			НоваяСтрока.КадастровыйНомер = КадастровыйНомер;
			
			СтрокаНомер = СтрокаНомер + 1;
			КадастровыйНомер = ЛистДокумента.Область(СтрокаНомер, СтолбецНомер).Текст;
		КонецЦикла;
	КонецЕсли; 
КонецПроцедуры 

#КонецОбласти
