#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СтрокиСлоевПоказателя = Параметры.Показатель.СлоиКарты.НайтиСтроки(Новый Структура("СлойКарты", Параметры.ОбъектКарты.Слой));
	Если СтрокиСлоевПоказателя.Количество() = 0 Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ЗапросДетализации = СтрокиСлоевПоказателя[0].Детализация;
	ЗаполнитьТаблицуОтчета(ЗапросДетализации);
	
	Заголовок = "Детализация значений показателя """ + Параметры.Показатель + """ по объекту """ + Параметры.ОбъектКарты + """";
	АвтоЗаголовок = Ложь;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ЗначениеРасшифровки = ПолучитьЗначениеРасшифровки(Расшифровка);
	Если ЗначениеРасшифровки <> Null И ЗначениеРасшифровки <> Неопределено Тогда
		ПоказатьЗначение(, ЗначениеРасшифровки);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицуОтчета(ЗапросДетализации)
	СКДНаСервере = Новый СхемаКомпоновкиДанных;
	
	ИсточникДанных = СКДНаСервере.ИсточникиДанных.Добавить();
	ИсточникДанных.Имя = "ИсточникДанных";
	ИсточникДанных.ТипИсточникаДанных = "Local";
	
	НаборДанных = СКДНаСервере.НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
	НаборДанных.Имя = "ОсновнойНабор";
	НаборДанных.ИсточникДанных = "ИсточникДанных";
	НаборДанных.Запрос = ЗапросДетализации;
	
	ПараметрСКД = СКДНаСервере.Параметры.Добавить();
	ПараметрСКД.Имя = "Объект";
	ПараметрСКД.Значение = Параметры.ОбъектКарты;
	ПараметрСКД.Использование = ИспользованиеПараметраКомпоновкиДанных.Всегда;
	
	ПараметрСКД = СКДНаСервере.Параметры.Добавить();
	ПараметрСКД.Имя = "Показатель";
	ПараметрСКД.Значение = Параметры.Показатель;
	ПараметрСКД.Использование = ИспользованиеПараметраКомпоновкиДанных.Всегда;
	
	ПараметрСКД = СКДНаСервере.Параметры.Добавить();
	ПараметрСКД.Имя = "Значение";
	ПараметрСКД.Значение = Параметры.Значение;
	ПараметрСКД.Использование = ИспользованиеПараметраКомпоновкиДанных.Всегда;
	
	ПараметрСКД = СКДНаСервере.Параметры.Добавить();
	ПараметрСКД.Имя = "Дата";
	ПараметрСКД.Значение = Параметры.Дата;
	ПараметрСКД.Использование = ИспользованиеПараметраКомпоновкиДанных.Всегда;
	
	ДатаНачала = Справочники.гисПоказатели.ПолучитьНачалоПериода(Параметры.Показатель, Параметры.Дата);
	ПараметрСКД = СКДНаСервере.Параметры.Добавить();
	ПараметрСКД.Имя = "ДатаНачала";
	ПараметрСКД.Значение = ДатаНачала;
	ПараметрСКД.Использование = ИспользованиеПараметраКомпоновкиДанных.Всегда;
	
	
	// Это делается для того, чтобы загрузились доступные поля. Почему-то загружаются только так.
	АдресСКД = ПоместитьВоВременноеХранилище(СКДНаСервере, УникальныйИдентификатор);
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСКД));
	КомпоновщикНастроек.ЗагрузитьНастройки(СКДНаСервере.НастройкиПоУмолчанию);
	СКДНаСервере = ПолучитьИзВременногоХранилища(АдресСКД);
	
	Для Каждого Элемент Из КомпоновщикНастроек.Настройки.Выбор.ДоступныеПоляВыбора.Элементы Цикл
		Если Элемент.Папка Тогда
			Продолжить;
		КонецЕсли;
		
		ПолеНабораДанных = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
		ПолеНабораДанных.Поле = Строка(Элемент.Поле);
		ПолеНабораДанных.ПутьКДанным = Строка(Элемент.Поле);
		ПолеНабораДанных.Заголовок = Элемент.Заголовок;
	КонецЦикла;
	
	ГруппировкаОтчета = КомпоновщикНастроек.Настройки.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	ГруппировкаОтчета.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	ГруппировкаОтчета.Использование = Истина;
	
	ЗапросПервый1 = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1 * ИЗ (" + ЗапросДетализации + ") КАК Таблица");
	ЗапросПервый1.УстановитьПараметр("Объект", Параметры.ОбъектКарты);
	ЗапросПервый1.УстановитьПараметр("Показатель", Параметры.Показатель);
	ЗапросПервый1.УстановитьПараметр("Значение", Параметры.Значение);
	ЗапросПервый1.УстановитьПараметр("Дата", Параметры.Дата);
	ЗапросПервый1.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Попытка
		ТаблицаПервый1 = ЗапросПервый1.Выполнить().Выгрузить();
	Исключение
		ЗапросПервый1.Текст = ЗапросДетализации;
		ТаблицаПервый1 = ЗапросПервый1.Выполнить().Выгрузить();
	КонецПопытки;
	
	Для Каждого Колонка Из ТаблицаПервый1.Колонки Цикл
		ВыбранноеПоле = КомпоновщикНастроек.Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
		ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных(Колонка.Имя);
		ВыбранноеПоле.Заголовок = Колонка.Имя;
	КонецЦикла;
	
	// вывод готового отчета в табличный документ
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	ДанныеРасшифровкиОбъект = Новый ДанныеРасшифровкиКомпоновкиДанных;
	Попытка
		МакетКомпоновки = КомпоновщикМакета.Выполнить(СКДНаСервере, КомпоновщикНастроек.ПолучитьНастройки(), ДанныеРасшифровкиОбъект);
	Исключение
		Инфо = ИнформацияОбОшибке();
		Сообщить(КраткоеПредставлениеОшибки(Инфо));
		Возврат;
	КонецПопытки;
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровкиОбъект);
	
	ДанныеРасшифровки = ПоместитьВоВременноеХранилище(ДанныеРасшифровкиОбъект, УникальныйИдентификаторРасшифровки);
	
	Результат.Очистить();
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(Результат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
КонецПроцедуры

&НаСервере
Функция ПолучитьЗначениеРасшифровки(Знач Расшифровка)
	ДанныеРасшифровкиОбъект = ПолучитьИзВременногоХранилища(ДанныеРасшифровки);
	ЗначениеРасшифровки = ДанныеРасшифровкиОбъект.Элементы[Расшифровка].ПолучитьПоля()[0].Значение;
	Возврат ЗначениеРасшифровки;
КонецФункции

#КонецОбласти
