#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПараметрыСвязи = Константы.гисЗащитаПараметрыСвязи.Получить().Получить();
	Если ЗначениеЗаполнено(ПараметрыСвязи) Тогда
		Адрес = ПараметрыСвязи.Адрес;
		Порт = ПараметрыСвязи.Порт;
	Иначе
		Адрес = "localhost";
		Порт = 9099;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьИзменения(Команда)
	Если СохранитьИзмененияНаСервере() Тогда
		ЭтотОбъект.Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьСоединение(Команда)
	СообщениеОбОшибке = "";
	Если ПроверитьЛицензиюСеанса(СообщениеОбОшибке) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Соединение успешно установлено!");
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю("Ошибка при установке соединения!" + Символы.ПС + СообщениеОбОшибке);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьПараметрыСвязи()
	Если Не ЗначениеЗаполнено(Адрес) Тогда
		ОбщегоНазначения.СообщитьПользователю("Не указан адрес!", , "Адрес");
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Новый Структура("Адрес,Порт", Адрес, ?(ЗначениеЗаполнено(Порт), Порт, 9099));
КонецФункции
	
&НаСервере
Функция СохранитьИзмененияНаСервере()
	РезультатСохранения = Истина;
	
	ПараметрыСвязи = ПолучитьПараметрыСвязи();
	Если ПараметрыСвязи <> Неопределено Тогда
		Попытка
			Константы.гисЗащитаПараметрыСвязи.Установить(Новый ХранилищеЗначения(ПараметрыСвязи));
		Исключение
			РезультатСохранения = Ложь;
			ОбщегоНазначения.СообщитьПользователю("Ошибка при сохранении параметров подключения к серверу СЛК!" + Символы.ПС + ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;
	
	Возврат РезультатСохранения;
КонецФункции

&НаСервере
Функция ПроверитьЛицензиюСеанса(СообщениеОбОшибке)
	ПараметрыСвязи = ПолучитьПараметрыСвязи();
	Возврат гисСлкМенедежерЗащиты.ПроверитьЛицензиюСеанса(СообщениеОбОшибке, ПараметрыСвязи);
КонецФункции

#КонецОбласти
