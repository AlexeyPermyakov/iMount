
#Область ПрограммныйИнтерфейс

Процедура ПриСозданииНаСервере(Форма, ДополнительныеПараметры) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОтрисоватьФормуЧата(Форма, ДополнительныеПараметры);
	ТаблицаСостава = ПолучитьСоставЧатов(ДополнительныеПараметры.КонтекстЧата);
		
	Форма.ТекущиеЧаты.Очистить();
	
	Для Каждого СтрТЗ Из ТаблицаСостава Цикл
		НоваяСтрока = Форма.ТекущиеЧаты.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрТЗ);
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьТекстОкнаСообщений(ИдентификаторЧата) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекПользователь = Пользователи.ТекущийПользователь();
	
	ТекстЧата = "<html><body bgcolor=""WhiteSmoke""><font face=""Arial""><table width=""100%"">";
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	лм_СообщенияЧатов.ТекстСообщения КАК ТекстСообщения,
	               |	лм_СообщенияЧатов.ДатаСообщения КАК ДатаСообщения,
	               |	лм_СообщенияЧатов.АвторСообщения КАК АвторСообщения,
	               |	лм_СообщенияЧатов.НомерСообщения КАК НомерСообщения
	               |ИЗ
	               |	РегистрСведений.лм_СообщенияЧатов КАК лм_СообщенияЧатов
	               |ГДЕ
	               |	лм_СообщенияЧатов.ИдентификаторЧата = &ИдентификаторЧата
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ДатаСообщения";
	
	Запрос.УстановитьПараметр("ИдентификаторЧата", ИдентификаторЧата);	
	
	Выборка = Запрос.Выполнить().Выбрать();	
	
	Пока Выборка.Следующий() Цикл
		ПоложениеСообщения = ?(Выборка.АвторСообщения = ТекПользователь, "right", "left");
		ЦветСообщения      = ?(Выборка.АвторСообщения = ТекПользователь, "Honeydew", "White");
		НачальнаяВставка   = ?(Выборка.АвторСообщения = ТекПользователь, "</td><td width=""50%""></td><td>&nbsp&nbsp&nbsp</td>", "");
		КонечнаяВставка    = ?(Выборка.АвторСообщения = ТекПользователь, "", "</td><td>&nbsp&nbsp&nbsp</td><td width=""50%""></td>");
		ТекстЧата = ТекстЧата + "<tr>" + НачальнаяВставка + "<td align=""" + ПоложениеСообщения + """ style = ' border-radius: 50px 50px 50px 50px; background: " + ЦветСообщения + ";'><font size=""1""><i>" + Выборка.АвторСообщения + "&nbsp" + Формат(Выборка.ДатаСообщения, "ДЛФ=DT") + " #" + Выборка.НомерСообщения + "</i></font><br>" + Выборка.ТекстСообщения + "</td>" + КонечнаяВставка + "</tr></tr>";
	КонецЦикла;
	
	ТекстЧата = ТекстЧата + "</table></font></body></html>";
	
	Возврат ТекстЧата;
		
КонецФункции

Процедура ОтправитьСообщение(ИдентификаторЧата, ТекстСообщения, ВладелецЧата) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НовоеСообщение                        = РегистрыСведений.лм_СообщенияЧатов.СоздатьМенеджерЗаписи();
	НовоеСообщение.ИдентификаторЧата      = ИдентификаторЧата;
	НовоеСообщение.ТекстСообщения         = ТекстСообщения;
	НовоеСообщение.АвторСообщения         = Пользователи.ТекущийПользователь();
	НовоеСообщение.ИдентификаторСообщения = Новый УникальныйИдентификатор;
	НовоеСообщение.НомерСообщения         = ПолучитьНомерСообщения(ИдентификаторЧата, НовоеСообщение.ИдентификаторСообщения);
	НовоеСообщение.ДатаСообщения          = ТекущаяДата();
	
	Если ВладелецЧата <> Неопределено Тогда
		НовоеСообщение.ВладелецЧата = ВладелецЧата;
	КонецЕсли;
		
	НовоеСообщение.Записать();
		
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Функция ПолучитьНомерСообщения(ИдентификаторЧата, ИдентификаторСообщения)
	
	Запрос       = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	МАКСИМУМ(лм_СообщенияЧатов.НомерСообщения) КАК НомерСообщения
	               |ИЗ
	               |	РегистрСведений.лм_СообщенияЧатов КАК лм_СообщенияЧатов
	               |ГДЕ
	               |	лм_СообщенияЧатов.ИдентификаторЧата = &ИдентификаторЧата";
	
	Запрос.УстановитьПараметр("ИдентификаторЧата", ИдентификаторЧата);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий();		
	Если Выборка.НомерСообщения <> Null Тогда
		Возврат Выборка.НомерСообщения + 1;
	Иначе
		Возврат 1;
	КонецЕсли;
		
КонецФункции

Функция ПолучитьСоставЧатов(КонтекстЧата)
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	Запрос       = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	лм_СоставЧатов.ИдентификаторЧата КАК ИдентификаторЧата,
	               |	лм_СоставЧатов.Пользователь КАК Пользователь
	               |ИЗ
	               |	РегистрСведений.лм_СоставЧатов КАК лм_СоставЧатов
	               |ГДЕ
	               |	лм_СоставЧатов.КонтекстЧата = &КонтекстЧата
	               |ИТОГИ ПО
	               |	ИдентификаторЧата";
	
	Запрос.УстановитьПараметр("КонтекстЧата", КонтекстЧата);
	
	ТаблицаРезультат = Новый ТаблицаЗначений;
	ТаблицаРезультат.Колонки.Добавить("ИдентификаторЧата");
	ТаблицаРезультат.Колонки.Добавить("НаименованиеЧата");
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока Выборка.Следующий() Цикл
		ВыборкаПользователи = Выборка.Выбрать();
		НаименованиеЧата    = "";
		ВходитВЧат          = Ложь;
		Пока ВыборкаПользователи.Следующий() Цикл
			Если ВыборкаПользователи.Пользователь = ТекущийПользователь Тогда
				ВходитВЧат = Истина;
			Иначе	
				НаименованиеЧата = НаименованиеЧата + ?(ЗначениеЗаполнено(НаименованиеЧата), ", ", "") + ВыборкаПользователи.Пользователь.Наименование;
			КонецЕсли;
		КонецЦикла;
		
		Если ВходитВЧат Тогда
			СтрТЗ                   = ТаблицаРезультат.Добавить();
			СтрТЗ.ИдентификаторЧата = Выборка.ИдентификаторЧата;
			СтрТЗ.НаименованиеЧата  = НаименованиеЧата;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТаблицаРезультат;
	
КонецФункции

Процедура ОтрисоватьФормуЧата(Форма, ДополнительныеПараметры)
	
	ГруппаДляОтрисовки = Форма.Элементы.Найти(ДополнительныеПараметры.ИмяГруппыФормы);
	
	Если ГруппаДляОтрисовки = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	ЭлементыФормы = Форма.Элементы;
	
#Область Реквизиты

	/////   РЕКВИЗИТЫ   //////////

	МассивДобавляемыхРеквизитов = Новый Массив;

	// ******   Реквизит формы "ТекущиеЧаты"    ***********

	НовыйРеквизит = Новый РеквизитФормы("ТекущиеЧаты",Новый ОписаниеТипов("ТаблицаЗначений"));
	НовыйРеквизит.Заголовок = "Текущие чаты";
	НовыйРеквизит.СохраняемыеДанные = Ложь;
	МассивДобавляемыхРеквизитов.Добавить(НовыйРеквизит);

	// ******   Реквизит формы "ИдентификаторЧата"    ***********

	НовыйРеквизит = Новый РеквизитФормы("ИдентификаторЧата",Новый ОписаниеТипов("УникальныйИдентификатор"), "ТекущиеЧаты");
	НовыйРеквизит.Путь = "ТекущиеЧаты";
	НовыйРеквизит.Заголовок = "Идентификатор чата";
	НовыйРеквизит.СохраняемыеДанные = Ложь;
	МассивДобавляемыхРеквизитов.Добавить(НовыйРеквизит);

	// ******   Реквизит формы "НаименованиеЧата"    ***********

	НовыйРеквизит = Новый РеквизитФормы("НаименованиеЧата",Новый ОписаниеТипов("Строка"), "ТекущиеЧаты");
	НовыйРеквизит.Путь = "ТекущиеЧаты";
	НовыйРеквизит.Заголовок = "Наименование чата";
	НовыйРеквизит.СохраняемыеДанные = Ложь;
	МассивДобавляемыхРеквизитов.Добавить(НовыйРеквизит);

	// ******   Реквизит формы "ТекстЧата"    ***********

	НовыйРеквизит = Новый РеквизитФормы("ТекстЧата",Новый ОписаниеТипов("Строка"));
	НовыйРеквизит.Заголовок = "Текст чата";
	НовыйРеквизит.СохраняемыеДанные = Ложь;
	МассивДобавляемыхРеквизитов.Добавить(НовыйРеквизит);

	// ******   Реквизит формы "ТекстСообщения"    ***********

	НовыйРеквизит = Новый РеквизитФормы("ТекстСообщения",Новый ОписаниеТипов("Строка"));
	НовыйРеквизит.Заголовок = "Текст сообщения";
	НовыйРеквизит.СохраняемыеДанные = Ложь;
	МассивДобавляемыхРеквизитов.Добавить(НовыйРеквизит);


	Форма.ИзменитьРеквизиты(МассивДобавляемыхРеквизитов);

#КонецОбласти

#Область Команды

	/////   КОМАНДЫ   //////////

	// ******   Команда формы "ОтправитьСообщение"    ***********

	НоваяКоманда = Форма.Команды.Добавить("ОтправитьСообщение");
	НоваяКоманда.Действие = "ОтправитьСообщение";
	НоваяКоманда.Заголовок = "Отправить сообщение";
	НоваяКоманда.ИзменяетСохраняемыеДанные = Ложь;
	НоваяКоманда.Отображение = ОтображениеКнопки.Авто;
	НоваяКоманда.Подсказка = "Отправить сообщение";
	
#КонецОбласти

#Область Элементы

	/////   ЭЛЕМЕНТЫ   //////////

	// ******   Группа формы "ГруппаЧатОсновная"    ***********

	НовыйЭлемент = Форма.Элементы.Вставить("ГруппаЧатОсновная",Тип("ГруппаФормы"),Неопределено,Неопределено);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Заголовок = "Группа чат основная";
	НовыйЭлемент.Подсказка = "Группа чат основная";
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	НовыйЭлемент.ОтображатьЗаголовок = Ложь;
	НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;

	ЭлементыФормы.Переместить(НовыйЭлемент, ГруппаДляОтрисовки);
	
	// ******   Группа формы "ГруппаОкноЧата"    ***********

	НовыйЭлемент = Форма.Элементы.Вставить("ГруппаОкноЧата",Тип("ГруппаФормы"),Форма.Элементы.ГруппаЧатОсновная,Неопределено);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Заголовок = "Группа окно чата";
	НовыйЭлемент.Подсказка = "Группа окно чата";
	НовыйЭлемент.РастягиватьПоГоризонтали = Истина;
	НовыйЭлемент.ОтображатьЗаголовок = Ложь;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;

	// ******   Группа формы "ГруппаВводСообщения"    ***********

	НовыйЭлемент = Форма.Элементы.Вставить("ГруппаВводСообщения",Тип("ГруппаФормы"),Форма.Элементы.ГруппаОкноЧата,Неопределено);
	НовыйЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.Заголовок = "Группа ввод сообщения";
	НовыйЭлемент.Подсказка = "Группа ввод сообщения";
	НовыйЭлемент.РастягиватьПоГоризонтали = Истина;
	НовыйЭлемент.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	НовыйЭлемент.ОтображатьЗаголовок = Ложь;
	НовыйЭлемент.Отображение = ОтображениеОбычнойГруппы.Нет;

	// ******   Кнопка формы "ОтправитьСообщение"    ***********

	НовыйЭлемент = Форма.Элементы.Вставить("ОтправитьСообщение",Тип("КнопкаФормы"),Форма.Элементы.ГруппаВводСообщения,Неопределено);
	НовыйЭлемент.ИмяКоманды = "ОтправитьСообщение";

	// ******   Поле формы "ТекстСообщения"    ***********

	НовыйЭлемент = Форма.Элементы.Вставить("ТекстСообщения",Тип("ПолеФормы"),Форма.Элементы.ГруппаВводСообщения,Форма.Элементы.ОтправитьСообщение);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	НовыйЭлемент.ПутьКДанным = "ТекстСообщения";
	НовыйЭлемент.АвтомаксимальнаяШирина = Ложь;
	НовыйЭлемент.МногострочныйРежим = Истина;

	// ******   Поле формы "ТекстЧата"    ***********

	НовыйЭлемент = Форма.Элементы.Вставить("ТекстЧата",Тип("ПолеФормы"),Форма.Элементы.ГруппаОкноЧата,Форма.Элементы.ГруппаВводСообщения);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеHTMLДокумента;
	НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	НовыйЭлемент.ПутьКДанным = "ТекстЧата";

	// ******   Таблица формы "УчастникиЧата"    ***********

	НовыйЭлемент = Форма.Элементы.Вставить("УчастникиЧата",Тип("ТаблицаФормы"),Форма.Элементы.ГруппаЧатОсновная,Форма.Элементы.ГруппаОкноЧата);
	НовыйЭлемент.ПутьКДанным = "ТекущиеЧаты";
	НовыйЭлемент.АвтоВводНовойСтроки = Истина;
	НовыйЭлемент.ВертикальныеЛинии = Ложь;
	НовыйЭлемент.ВысотаВСтрокахТаблицы = 3;
	НовыйЭлемент.ГоризонтальныеЛинии = Ложь;
	НовыйЭлемент.ИзменятьПорядокСтрок = Ложь;
	НовыйЭлемент.ИзменятьСоставСтрок = Ложь;
	НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	НовыйЭлемент.РежимВыделения = РежимВыделенияТаблицы.Множественный;
	НовыйЭлемент.УстановитьДействие("ПриАктивизацииСтроки", "УчастникиЧатаПриАктивизацииСтроки");
	НовыйЭлемент.Видимость = ДополнительныеПараметры.ОтображатьСписокЧатов;

	// ******   Поле формы "УчастникиЧатаНаименованиеЧата"    ***********

	НовыйЭлемент = Форма.Элементы.Вставить("УчастникиЧатаНаименованиеЧата",Тип("ПолеФормы"),Форма.Элементы.УчастникиЧата,Неопределено);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.Заголовок = "Чат";
	НовыйЭлемент.ПутьКДанным = "ТекущиеЧаты.НаименованиеЧата";	
	
#КонецОбласти	
	
КонецПроцедуры

#КонецОбласти