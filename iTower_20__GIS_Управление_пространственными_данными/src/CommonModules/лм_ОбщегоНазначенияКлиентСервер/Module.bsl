
#Область ПрограммныйИнтерфейс

// Выполняет поиск в массиве структур, аналогично функции "НайтиСтроки()" в таблице значений
//
// Параметры:
// Исходный массив - массив структур, в котором производится поиск
// Отбор - структура с условиями поиска: ключ структуры определяет имя "колонки", по которой будет осуществляться поиск,
// а значение структуры - искомое значение.
// 
// Возвращаемое значение:
// Массив структур, удовлетворяющих условиям отбора
//
Функция НайтиСтрокиТаблицыОбъекта(ИсходныйМассив, Отбор) Экспорт
	
	МассивРезультат = Новый Массив;
	
	Если ИсходныйМассив.Количество() = 0 Тогда
		Возврат МассивРезультат;
	КонецЕсли;
	
	МассивКлючейОтбора = Новый Массив;
	
	Для Каждого КлючИЗначение Из Отбор Цикл
		МассивКлючейОтбора.Добавить(КлючИЗначение.Ключ);	
	КонецЦикла;
	
	Для Каждого Стр Из ИсходныйМассив Цикл
		Подходит = Истина;
		Для Каждого Ключ Из МассивКлючейОтбора Цикл
			Если Стр[Ключ] <> Отбор[Ключ] Тогда
				Подходит = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если Подходит Тогда
			МассивРезультат.Добавить(Стр);	
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивРезультат;
	
КонецФункции

#КонецОбласти