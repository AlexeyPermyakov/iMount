#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	СписокОбъектовКарты = ПолучитьСписокОбъектовКарты(ПараметрКоманды);
	Если СписокОбъектовКарты.Количество() = 0 Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ВопросОткрытьКартуЗавершение", ЭтотОбъект, Новый Структура("ОбъектУчета", ПараметрКоманды)), 
			"Объект не привязан к объекту на карте! Создать объект на карте?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	ИначеЕсли СписокОбъектовКарты.Количество() > 1 Тогда
		ФормаВыбора = ПолучитьФорму("Справочник.гисОбъектыКарты.ФормаВыбора", Новый Структура("Отбор", Новый Структура("Ссылка", СписокОбъектовКарты)));
		ФормаВыбора.Элементы.Список.Отображение = ОтображениеТаблицы.Список;
		
		ФормаВыбора.ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ВыборОбъектаЗавершение", ЭтотОбъект, Новый Структура("ПараметрыВыполненияКоманды", ПараметрыВыполненияКоманды));
		ФормаВыбора.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ФормаВыбора.Открыть();
	Иначе
		ПоказатьОбъектКарты(СписокОбъектовКарты[0].Значение, ПараметрыВыполненияКоманды);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВопросОткрытьКартуЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ОбъектУчета = ДополнительныеПараметры.ОбъектУчета;
		Слой = ПолучитьСлойНаСервере(ОбъектУчета);
		ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", Новый Структура("Наименование,ОбъектУчета,Слой", Строка(ОбъектУчета), ОбъектУчета, Слой));
		ОткрытьФорму("Справочник.гисОбъектыКарты.ФормаОбъекта", ПараметрыФормы);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыборОбъектаЗавершение(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт
	Если ВыбранноеЗначение <> Неопределено Тогда
		ПоказатьОбъектКарты(ВыбранноеЗначение, ДополнительныеПараметры.ПараметрыВыполненияКоманды);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьСписокОбъектовКарты(ОбъектУчета)
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	гисОбъектыКарты.Ссылка
	|ИЗ
	|	Справочник.гисОбъектыКарты КАК гисОбъектыКарты
	|ГДЕ
	|	гисОбъектыКарты.ОбъектУчета = &ОбъектУчета";
	Запрос.УстановитьПараметр("ОбъектУчета", ОбъектУчета);
	МассивОбъектовКарты = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	СписокОбъектовКарты = Новый СписокЗначений;
	СписокОбъектовКарты.ЗагрузитьЗначения(МассивОбъектовКарты);
	Возврат СписокОбъектовКарты;
КонецФункции

&НаСервере
Функция ПолучитьСлойНаСервере(ОбъектУчета)
	СправочникОбъектаУчета = ОбъектУчета.Метаданные();
	
	// получаем все слои (с выбранным владельцем и пустым)
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ Ссылка ИЗ Справочник.гисСлоиКарты ГДЕ НЕ ПометкаУдаления И СправочникОбъектовУчета = &Имя");
	Запрос.УстановитьПараметр("Имя", СправочникОбъектаУчета.Имя);
	ТаблицаСлоев = Запрос.Выполнить().Выгрузить();
	
	// оставляем только слои с пустым владельцем
	МассивСлоев = Новый Массив;
	Для Каждого СтрокаТаблицы Из ТаблицаСлоев Цикл
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.Ссылка.СправочникОбъектовУчетаВладелец) Тогда
			МассивСлоев.Добавить(СтрокаТаблицы.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	// если есть владельцы, добавляем слои с указанным нужным владельцем
	Если СправочникОбъектаУчета.Владельцы.Количество() > 0 Тогда
		Владелец = ОбъектУчета.Владелец;
		Если ЗначениеЗаполнено(Владелец) Тогда
			Запрос.Текст = Запрос.Текст + " И СправочникОбъектовУчетаВладелец = &Владелец";
			Запрос.УстановитьПараметр("Владелец", Владелец);
			ТаблицаСлоев = Запрос.Выполнить().Выгрузить();
			Для Каждого Строка Из ТаблицаСлоев Цикл
				МассивСлоев.Добавить(Строка.Ссылка);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Слой = Неопределено;
	Если МассивСлоев.Количество() = 1 Тогда
		Слой = МассивСлоев[0];
	КонецЕсли;
	
	Возврат Слой;
КонецФункции


&НаКлиенте
Процедура ПоказатьОбъектКарты(ОбъектКарты, ПараметрыВыполненияКоманды)
	НашлиКарту = Ложь;
	
	Окна = ПолучитьОкна();
	Для Каждого Окно Из Окна Цикл
		Для Каждого Содержимое Из Окно.Содержимое Цикл
			Если ТипЗнч(Содержимое) = Тип("УправляемаяФорма") И Содержимое.ИмяФормы = "Обработка.гисКарта.Форма.Форма" Тогда
				НашлиКарту = Истина;
				Оповестить("КартаПоказатьОбъект", ОбъектКарты);
				Содержимое.Активизировать();
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Если Не НашлиКарту Тогда 
		ПараметрыФормы = Новый Структура("ОбъектКарты", ОбъектКарты);
		ОткрытьФорму("Обработка.гисКарта.Форма", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно, ПараметрыВыполненияКоманды.НавигационнаяСсылка);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
