
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если ЗначениеЗаполнено(Объект.ПутьКФайлу) Тогда
		ФайлКартинки = Новый Файл(Объект.ПутьКФайлу);
		РасширениеФайлаКартинки = ФайлКартинки.Расширение;
	КонецЕсли;
	Картинка = ПоместитьВоВременноеХранилище(Base64Значение(Объект.Содержимое), УникальныйИдентификатор);
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ЭтоАдресВременногоХранилища(Картинка) Тогда
		ТекущийОбъект.Содержимое = Base64Строка(ПолучитьИзВременногоХранилища(Картинка));
	Иначе
		ТекущийОбъект.Содержимое = Картинка;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ТекущийОбъект.Код) Тогда
		ТекущийОбъект.УстановитьНовыйКод();
	КонецЕсли;
	ТекущийОбъект.ПутьКФайлу = "user" + ПолучитьРазделительПути() + ТекущийОбъект.Код + "_" + Формат(ТекущаяДата(), "ДФ=""ггггММддЧЧммсс""") + РасширениеФайлаКартинки;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КартинкаЗагрузить(Команда)
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.Показать(Новый ОписаниеОповещения("КартинкаЗагрузитьНачало", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура КартинкаЗагрузитьНачало(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	Если ВыбранныеФайлы <> Неопределено Тогда
		ВыбранныйФайл = ВыбранныеФайлы[0];
		
		ФайлКартинки = Новый Файл(ВыбранныйФайл);
		РасширениеФайлаКартинки = ФайлКартинки.Расширение;
		Если НЕ ЗначениеЗаполнено(Объект.Наименование) Тогда
			Объект.Наименование = ФайлКартинки.ИмяБезРасширения;
		КонецЕсли;
		
		НачатьПомещениеФайла(Новый ОписаниеОповещения("КартинкаЗагрузитьЗавершение", ЭтотОбъект), , ВыбранныйФайл, Ложь, УникальныйИдентификатор);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КартинкаЗагрузитьЗавершение(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	Если Результат Тогда
		Картинка = Адрес;
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура КартинкаСохранить(Команда)
	ПолучаемыеФайлы = Новый Массив;
	ПолучаемыеФайлы.Добавить(Новый ОписаниеПередаваемогоФайла(Объект.Наименование, Картинка));
	
	ДиалогСохраненияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогСохраненияФайла.МножественныйВыбор = Ложь;
	Фильтр = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Картинки точечных объектов (*%1)|*%1'"), РасширениеФайлаКартинки);
	ДиалогСохраненияФайла.Фильтр = Фильтр;
	
	НачатьПолучениеФайлов(Новый ОписаниеОповещения("СохранитьФайлЗавершение", ЭтотОбъект), ПолучаемыеФайлы, ДиалогСохраненияФайла, Истина);
КонецПроцедуры

&НаКлиенте
Процедура СохранитьФайлЗавершение(ПолученныеФайлы, ДополнительныеПараметры) Экспорт
	Возврат;
КонецПроцедуры


&НаКлиенте
Процедура КартинкаУдалить(Команда)
	Картинка = Неопределено;
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти
