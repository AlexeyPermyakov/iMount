#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	КодСлоя = Неопределено;
	ГеометрияДобавленная = Неопределено;
	Если Параметры.Свойство("КодСлоя", КодСлоя) И Параметры.Свойство("ГеометрияДобавленная", ГеометрияДобавленная) И ТипЗнч(ГеометрияДобавленная) = Тип("Массив") Тогда
		Для Каждого Геометрия Из ГеометрияДобавленная Цикл
			СтрокаТаблицы = НовыеГометрии.Добавить();
			СтрокаТаблицы.Идентификатор = Геометрия.properties._id_;
			Геометрия.properties.Удалить("_layer_");
			Геометрия.properties.Удалить("_id_");
			СтрокаТаблицы.Геометрия = Геометрия;
			Слой = Справочники.гисСлоиКарты.НайтиПоКоду( КодСлоя );
			Если ЗначениеЗаполнено(Слой) Тогда;
				СтрокаТаблицы.Слой = Слой.Ссылка;
				СтрокаТаблицы.ТипГеометрии = Геометрия.geometry.type;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Закрыть( 0 < НовыеГометрии.Количество() );
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СозданиеОбъектаПоГеометрии(Команда)
	Если Элементы.НовыеГеометрии.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	НоваяГеометрия = Элементы.НовыеГеометрии.ТекущиеДанные;
	
	Если НоваяГеометрия.ТипГеометрии = "Point" Тогда
		ЗначенияЗаполнения = Новый Структура("Слой,Широта,Долгота", 
			НоваяГеометрия.Слой, 
			НоваяГеометрия.Геометрия.geometry.coordinates[1], 
			НоваяГеометрия.Геометрия.geometry.coordinates[0]
		);
	Иначе
		ЗначенияЗаполнения = Новый Структура("Слой,Наименование,ГеометрияWgs", 
			НоваяГеометрия.Слой, 
			НоваяГеометрия.Идентификатор, 
			гисРаботаСJson.ЗаписатьНативно(НоваяГеометрия.Геометрия)
		);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	Форма = ПолучитьФорму("Справочник.гисОбъектыКарты.ФормаОбъекта", ПараметрыФормы);
	Форма.ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("СозданиеОбъектаПоГеометрииЗавершение", ЭтотОбъект, Новый Структура("Форма", Форма));
	Форма.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	Форма.Открыть();
КонецПроцедуры

&НаКлиенте
Процедура СозданиеОбъектаПоГеометрииЗавершение(Команда, ДополнительныеПараметры) Экспорт
	Если ЗначениеЗаполнено(ДополнительныеПараметры.Форма.Объект.Ссылка) Тогда
		Попытка
			СохраненныеГеометрии = Новый Массив;
			СохраненныеГеометрии.Добавить(
				Новый Структура( "id,cod",
					Элементы.НовыеГеометрии.ТекущиеДанные.Идентификатор,
					ДополнительныеПараметры.Форма.Объект.Код
				)
			);
			Элементы.НовыеГеометрии.ТекущиеДанные.Идентификатор = ДополнительныеПараметры.Форма.Объект.Код;
			гисРаботаСКартойКлиент.ОбъектыКартыУстановитьОтметки(ВладелецФормы, СохраненныеГеометрии);
			НовыеГометрии.Удалить( Элементы.НовыеГеометрии.ТекущиеДанные );
		Исключение
		КонецПопытки;
		
		ДополнительныеПараметры.Форма.РазблокироватьДанныеФормыДляРедактирования();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УдалениеГеометрии(Команда)
	Если Элементы.НовыеГеометрии.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ТекущаяГеометрия = Элементы.НовыеГеометрии.ТекущиеДанные;
	ПараметрыЗавершения = Новый Структура("ТаблицаГеометрий,ТекущаяГеометрия", НовыеГометрии, ТекущаяГеометрия);
	ОписаниеОповещения = Новый ОписаниеОповещения("УдалениеГеометрииЗавершение", ЭтотОбъект, ПараметрыЗавершения);
	ПоказатьВопрос(ОписаниеОповещения, "Удалить геометрию?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет, "Редактор геометрии");
КонецПроцедуры

&НаКлиенте
Процедура УдалениеГеометрииЗавершение(РезультатВопроса, ПараметрыЗавершения) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		УдаляемаяГеометрия = Новый Массив;
		УдаляемаяГеометрия.Добавить( Новый Структура( "id", ПараметрыЗавершения.ТекущаяГеометрия.Идентификатор ) );
		гисРаботаСКартойКлиент.ОбъектыКартыУстановитьОтметки(ВладелецФормы, УдаляемаяГеометрия);
		ПараметрыЗавершения.ТаблицаГеометрий.Удалить( ПараметрыЗавершения.ТекущаяГеометрия );
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыНовыеГеометрии

&НаКлиенте
Процедура НовыеГеометрииПриАктивизацииСтроки(Элемент)
	Если Элементы.НовыеГеометрии.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	гисРаботаСКартойКлиент.ОбъектыКартыРедактореВыделить( ВладелецФормы, Элементы.НовыеГеометрии.ТекущиеДанные.Идентификатор );
КонецПроцедуры

#КонецОбласти
