
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// МенюОтчеты
	
	МассивОбъектов = Новый Массив;
	Если ЗначениеЗаполнено(Объект.ОбъектУчета) Тогда
		МассивОбъектов.Добавить(Объект.ОбъектУчета.Метаданные());
	КонецЕсли;
	
	Попытка
		// зачем называть "СписокОбъектов", если это вообще не список??? у нас массив
		Выполнить("МенюОтчеты.ПриСозданииНаСервере(ЭтотОбъект, Элементы.ПодменюОтчеты, МассивОбъектов, Ложь);");
	Исключение
		// если модуля нет, просто не добавляем отчеты
	КонецПопытки;
	
	// Конец МенюОтчеты
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	СлойПриИзменении(Элементы.Слой);
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("ПослеЗаписиОбъекта", Объект.Ссылка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НеГеоОбъектПриИзменении(Элемент)
	УстановитьВидимостьЭлементов();
КонецПроцедуры

&НаКлиенте
Процедура СлойПриИзменении(Элемент)
	УстановитьВидимостьЭлементов();
КонецПроцедуры

&НаКлиенте
Процедура ОбъектУчетаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(Объект.Слой) Тогда
		ПараметрыСлоя = ПолучитьПараметрыСлоя(Объект.Слой);
		Если ЗначениеЗаполнено(ПараметрыСлоя.СправочникОбъектовУчета) Тогда
			ПараметрыФормы = Новый Структура("ТекущаяСтрока,РежимВыбора", Объект.ОбъектУчета, Истина);
			Если ЗначениеЗаполнено(ПараметрыСлоя.СправочникОбъектовУчетаВладелец) Тогда
				ПараметрыФормы.Вставить("Отбор", Новый Структура("Владелец", ПараметрыСлоя.СправочникОбъектовУчетаВладелец));
			КонецЕсли;
			
			Обработчик = Новый ОписаниеОповещения("ОбъектУчетаНачалоВыбораЗавершение", ЭтотОбъект);
			ОткрытьФорму("Справочник." + ПараметрыСлоя.СправочникОбъектовУчета + ".ФормаВыбора", ПараметрыФормы, , , , , 
				Обработчик, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		Иначе
			ТекстПредупреждения = НСтр("ru = 'У слоя ""%1"" не выбран справочник объектов учета!'");
			ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПредупреждения, Объект.Слой);
			ПоказатьПредупреждение(, ТекстПредупреждения);
		КонецЕсли;
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Не выбран слой!'"));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбъектУчетаНачалоВыбораЗавершение(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт
	Если ВыбранноеЗначение <> Неопределено Тогда
		Объект.ОбъектУчета = ВыбранноеЗначение;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуОтчет(Команда)
	
	Выполнить("МенюОтчетыКлиент.ВыполнитьПодключаемуюКомандуОтчет(Команда, ЭтотОбъект, Новый Структура(""Ссылка"", Объект.ОбъектУчета));");
	
КонецПроцедуры

&НаКлиенте
Процедура ГеометрияWgsРедактироватьВТаблице(Команда)
	ТипыГеометрии = Новый СписокЗначений();
	Если ЭтоПолигон(Объект.Слой) Тогда
		ТипыГеометрии.Добавить("Polygon");
		ТипыГеометрии.Добавить("MultiPolygon");
	ИначеЕсли ЭтоЛиния(Объект.Слой) Тогда
		ТипыГеометрии.Добавить("LineString");
		ТипыГеометрии.Добавить("MultiLineString");
	КонецЕсли;
	гисРаботаСПлощаднымиОбъектамиКлиент.ГеометрияРедактироватьВТаблице(ЭтотОбъект, "ГеометрияWgs", Элементы.ГеометрияWgsРедактироватьВТаблице, , ТипыГеометрии);
КонецПроцедуры

&НаКлиенте
Процедура ГеометрияWgsРедактироватьJson(Команда)
	гисРаботаСПлощаднымиОбъектамиКлиент.ГеометрияРедактироватьJson(ЭтотОбъект, "ГеометрияWgs")
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ЭтоПолигон(Тип)
	Возврат Тип.ТипГеометрии = Перечисления.гисТипыГеометрии.Полигон;
КонецФункции

&НаСервереБезКонтекста
Функция ЭтоЛиния(Тип)
	Возврат Тип.ТипГеометрии = Перечисления.гисТипыГеометрии.Линия;
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьПараметрыСлоя(Слой)
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Слой, "СправочникОбъектовУчета,СправочникОбъектовУчетаВладелец", Истина);
КонецФункции

&НаСервере
Процедура УстановитьВидимостьЭлементов()
	Элементы.ГруппаГеоОбъект.Видимость = Не Объект.НеГеоОбъект;
	
	Если Объект.НеГеоОбъект И ЗначениеЗаполнено(Объект.Слой) Тогда
		Объект.Слой = Неопределено;
	КонецЕсли;
	
	Если Объект.НеГеоОбъект И ЗначениеЗаполнено(Объект.ОбъектУчета) Тогда
		Объект.ОбъектУчета = Неопределено;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Слой)  Тогда
		Элементы.ГруппаТочка.Видимость = Ложь;
		Элементы.ГруппаЛинияПолигон.Видимость = Ложь;
		Если ЗначениеЗаполнено(Объект.Широта) Тогда
			Объект.Широта = "";
		КонецЕсли;
		Если ЗначениеЗаполнено(Объект.Долгота) Тогда
			Объект.Долгота = "";
		КонецЕсли;
		Если ЗначениеЗаполнено(Объект.ГеометрияWgs) Тогда
			Объект.ГеометрияWgs = "";
		КонецЕсли;
	ИначеЕсли Объект.Слой.ТипГеометрии = Перечисления.гисТипыГеометрии.Точка Тогда
		Элементы.ГруппаТочка.Видимость = Истина;
		Элементы.ГруппаЛинияПолигон.Видимость = Ложь;
		Если ЗначениеЗаполнено(Объект.ГеометрияWgs) Тогда
			Объект.ГеометрияWgs = "";
		КонецЕсли;
	ИначеЕсли Объект.Слой.ТипГеометрии = Перечисления.гисТипыГеометрии.Линия Тогда
		Элементы.ГруппаТочка.Видимость = Ложь;
		Элементы.ГруппаЛинияПолигон.Видимость = Истина;
		Если ЗначениеЗаполнено(Объект.Широта) Тогда
			Объект.Широта = "";
		КонецЕсли;
		Если ЗначениеЗаполнено(Объект.Долгота) Тогда
			Объект.Долгота = "";
		КонецЕсли;
	ИначеЕсли Объект.Слой.ТипГеометрии = Перечисления.гисТипыГеометрии.Полигон Тогда
		Элементы.ГруппаТочка.Видимость = Ложь;
		Элементы.ГруппаЛинияПолигон.Видимость = Истина;
		Если ЗначениеЗаполнено(Объект.Широта) Тогда
			Объект.Широта = "";
		КонецЕсли;
		Если ЗначениеЗаполнено(Объект.Долгота) Тогда
			Объект.Долгота = "";
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
