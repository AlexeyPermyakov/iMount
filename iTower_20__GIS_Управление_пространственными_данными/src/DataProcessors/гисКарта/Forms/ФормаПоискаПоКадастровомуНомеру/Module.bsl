#Область ОбработчикиСобытийЭлементовТаблицыФормыПоискПоРосрееструРезультаты

&НаКлиенте
Процедура ПоискПоРосрееструРезультатыПриАктивизацииСтроки(Элемент)
	Если Элементы.ПоискПоРосрееструРезультаты.ТекущиеДанные <> Неопределено Тогда
		ТекущаяСтрока = Элементы.ПоискПоРосрееструРезультаты.ТекущиеДанные;
		Если ЗначениеЗаполнено(ТекущаяСтрока.bbox) Тогда
			гисРаботаСКартойКлиент.РосреестрПодсветкаПоКадастровомуНомеру(ВладелецФормы, ТекущаяСтрока.КадастровыйНомер);
			гисРаботаСКартойКлиент.КартаЭкстентУстановить(ВладелецФормы, ТекущаяСтрока.bbox);
		Иначе
			Состояние("Не получены границы участка с Росреестра!");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоискПоРосрееструПодсказка(Команда)
	Подсказка = "Примеры использования:
	|31:16:127002:46 - один земельный участок.
	|31:16:127002:4% - все земельные участки, начинающиеся с 31:16:127002:4";
		
	ОткрытьФорму("ОбщаяФорма.гисФормаДляВыводаПодсказки", Новый Структура("Подсказка", Подсказка));
КонецПроцедуры


&НаКлиенте
Процедура ПоискПоРосрееструНайти(Команда)
	ПоискПоРосрееструОчиститьРезультаты(Команды.ПоискПоРосрееструОчиститьРезультаты);
	
	Попытка
		ОбъектПоиска = гисРаботаСКадастром.ПолучитьFeatures(ПоискПоРосрееструКадастровыйНомер);
		Атрибуты = ОбъектПоиска.attrs;
		НоваяСтрока = ПоискПоРосрееструРезультаты.Добавить();
		НоваяСтрока.КадастровыйНомер = Атрибуты.cn;
		НоваяСтрока.Адрес = Атрибуты.address;
		Если ОбъектПоиска.Свойство("extent") Тогда
			НоваяСтрока.bbox = гисРаботаСКадастром.СформироватьBbox(ОбъектПоиска.extent);
		КонецЕсли;
	Исключение
		ОбщегоНазначенияКлиент.СообщитьПользователю(ОписаниеОшибки());
	КонецПопытки;
КонецПроцедуры

&НаКлиенте
Процедура ПоискПоРосрееструОчиститьРезультаты(Команда)
	ПоискПоРосрееструРезультаты.Очистить();
	гисРаботаСКартойКлиент.РосреестрПодсветкаУбрать(ВладелецФормы);
КонецПроцедуры

#КонецОбласти
