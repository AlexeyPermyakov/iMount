#Область ОбработчикиСобытий

// Во всех открытых картах показывает переданный объект.
// Если ни одна карта не была открыта, то открывает карту и показывает на ней этот объект.
//
// Параметры:
//  ПараметрКоманды				 - СправочникСсылка.гисОбъектыКарты - объект для отображения на карте;
//  ПараметрыВыполненияКоманды	 - ПараметрыВыполненияКоманды - параметры выполнения команды.
//
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	НашлиКарту = Ложь;
	
	Окна = ПолучитьОкна();
	Для Каждого Окно Из Окна Цикл
		Для Каждого Содержимое Из Окно.Содержимое Цикл
			Если ТипЗнч(Содержимое) = Тип("УправляемаяФорма") И Содержимое.ИмяФормы = "Обработка.гисКарта.Форма.Форма" Тогда
				НашлиКарту = Истина;
				Если ТипЗНЧ(ПараметрКоманды) = Тип("СправочникСсылка.гисОбъектыКарты") Тогда
					Оповестить("КартаПоказатьОбъект", ПараметрКоманды);
				ИначеЕсли ТипЗНЧ(ПараметрКоманды) = Тип("СправочникСсылка.гисКадастровыеЗемельныеУчастки") Тогда
					Оповестить("КартаПоказатьУчасток", ПараметрКоманды);
				КонецЕсли;
				Содержимое.Активизировать();
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Если Не НашлиКарту Тогда 
		ПараметрыФормы = Новый Структура;
		Если ТипЗНЧ(ПараметрКоманды) = Тип("СправочникСсылка.гисОбъектыКарты") Тогда
			ПараметрыФормы.Вставить("ОбъектКарты", ПараметрКоманды);
		ИначеЕсли ТипЗНЧ(ПараметрКоманды) = Тип("СправочникСсылка.гисКадастровыеЗемельныеУчастки") Тогда
			ПараметрыФормы.Вставить("Участок", ПараметрКоманды);
		КонецЕсли;
		
		ОткрытьФорму("Обработка.гисКарта.Форма", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно, ПараметрыВыполненияКоманды.НавигационнаяСсылка);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
