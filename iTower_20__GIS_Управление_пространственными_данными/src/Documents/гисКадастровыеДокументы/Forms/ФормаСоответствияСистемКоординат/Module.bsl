
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Попытка
		МассивСистемКоординат = Параметры.МассивСистемКоординат;
		Для Каждого ЭлементМассива Из МассивСистемКоординат Цикл
			НоваяСтрока = СистемыКоординат.Добавить();
			НоваяСтрока.ИдСистемы = ЭлементМассива.Ид;
			НоваяСтрока.НазваниеСистемы = ЭлементМассива.Имя;
			Если ЗначениеЗаполнено(ЭлементМассива.Ссылка) Тогда
				НоваяСтрока.ПараметрыПеревода = ЗначениеИзСтрокиВнутр(ЭлементМассива.Ссылка);
			КонецЕсли;
		КонецЦикла;
	Исключение
		Отказ = Истина;
	КонецПопытки;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ВладелецФормы <> Неопределено И ТипЗнч(ВладелецФормы) = Тип("УправляемаяФорма") И ВладелецФормы.ИмяФормы = "Документ.гисКадастровыеДокументы.Форма.ФормаДокумента" Тогда
		КоличествоВидимыхЗакладок = 0;
		Для Каждого Элемент Из ВладелецФормы.Элементы.Страницы.ПодчиненныеЭлементы Цикл
			Если Элемент.Видимость Тогда
				КоличествоВидимыхЗакладок = КоличествоВидимыхЗакладок + 1;
			КонецЕсли;
		КонецЦикла;
		
		Если КоличествоВидимыхЗакладок > 2 Тогда
			Элементы.ДляВсехЗакладок.Видимость = Истина;
			ДляВсехЗакладок = Истина;
		КонецЕсли;
		
		Элементы.ОбновлятьОбъекты.Видимость = Истина;
		ОбновлятьОбъекты = Истина;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПрименитьИзменения(Команда)
	ЭтотОбъект.Закрыть(СформироватьВозвращаемоеЗначение());
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СформироватьВозвращаемоеЗначение()
	МассивСистемКоординат = Новый Массив;
	Для Каждого Строка Из СистемыКоординат Цикл
		МассивСистемКоординат.Добавить(Новый Структура("Ид,Имя,Ссылка", Строка.ИдСистемы, Строка.НазваниеСистемы, ЗначениеВСтрокуВнутр(Строка.ПараметрыПеревода)));
	КонецЦикла;
	Возврат Новый Структура("СистемыКоординат,ДляВсехЗакладок,ОбновлятьОбъекты", МассивСистемКоординат, ДляВсехЗакладок, ОбновлятьОбъекты);
КонецФункции

#КонецОбласти
